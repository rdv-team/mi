# Контроль целостности данных

# Назначение

Контроль целостности и полноты данных для целей сверки состояний между информационными базами интеграции. Контроль происходит за счет расчета хеша по ключевым реквизитам объекта в каждой из сторон интеграции.

### Настройка
- `Константы.рдв_ФормироватьХешОбъектов` - включает режим контроля данных, подсистема сохраняет контрольную сумму выгружаемого объекта, для сверки с внешней системой
### Настройка алгоритма расчета
- в созданном ранее модуле по шаблону `рдв_ФормированиеХешейОбъектовДемо` реализовать следующие методы:

```

// Инициализировать правила расчета хеша правилами согласно бизнес процессов
// 
// Параметры:
//  ПравилаРасчетаХеша - ТаблицаЗначений - таблица правил расчета хеша созданная на основании правил конвертации объектов
//
Процедура ИнициализироватьПравилаРасчетаХеша(ПравилаРасчетаХеша) Экспорт

	// Новое правило расчета хеша контрагента
	// В таблице правил появится 2 записи на контрагента.
	// В методе ниже ПравилаРасчетаХеша можно будет указать в каких случаях каким пользоваться
	// Если в ПравилаРасчетаХеша не будет задано, то механизм расчитаем по первым найденым
	ПравилоРасчетаХеша = ДобавитьПРХ(ПравилаРасчетаХеша, Метаданные.Справочники._ДемоКонтрагенты);
	ПравилоРасчетаХеша.ИмяПРХ = "Контрагент_ИННКПП";
	КонвертацияШапки = ПравилоРасчетаХеша.КонвертацияШапки;
	ДобавитьПРС(КонвертацияШапки, "ИНН");
	ДобавитьПРС(КонвертацияШапки, "КПП");

	// Меняем существующие правила по Заказу покупателя.
	// Реквизит контрагент будет расчитан на основании правил созданных выше.
	// Считаем что на Заказ несколько правил и устанавливаем правило для каждого.
	ПравилаПоМетаданным = НайтиПРХ(ПравилаРасчетаХеша, Метаданные.Документы._ДемоЗаказПокупателя);
	
	Для Каждого ПравилоРасчетаХеша Из ПравилаПоМетаданным Цикл
		
		КонвертацияШапки = ПравилоРасчетаХеша.КонвертацияШапки;
		Свойство = НайтиПРС(КонвертацияШапки, "Контрагент");
		Свойство.ИмяПРХ = "Контрагент_ИННКПП";
		
	КонецЦикла;

	// Для Организации будем считать несколько хешей
	// На основании правил конвертации и созданные
	ПравилоРасчетаХеша = ДобавитьПРХ(ПравилаРасчетаХеша, Метаданные.Справочники._ДемоОрганизации);
	ПравилоРасчетаХеша.ИмяПРХ = "Организация_ИННКПП";
	ПравилоРасчетаХеша.ВидХеша = Справочники.рдв_ВидыХешей.Предопределенный("ИННКПП");
	КонвертацияШапки = ПравилоРасчетаХеша.КонвертацияШапки;
	ДобавитьПРС(КонвертацияШапки, "ИНН");
	ДобавитьПРС(КонвертацияШапки, "КПП");
	
КонецПроцедуры

// Указыает по каким правил формировать хеш для объекта. Если не определено то правилам таблицы правил расчет по объекту метаданных
// 
// Параметры:
//  ОбъектОчереди - ЛюбаяСсылка - 
//  ПравилаРасчетаХеша - ТаблицаЗначений - см.рдв_МенеджерОбмена.ИнициализироватьПравилаКонвертации()
//  ПравилаДляРасчетаОбъекта - Массив из СтрокаТаблицыЗначений - массив для выбранных правил
//
Процедура ПравилаРасчетаХеша(ОбъектОчереди, ПравилаРасчетаХеша, ПравилаДляРасчетаОбъекта) Экспорт
	
	ОбъектМетаданных = ОбъектОчереди.Метаданные();
	
	// Т.к. контрагента 2 записи, то указываем системе по какому правилу считать саму ссылку
	Если ОбъектМетаданных = Метаданные.Справочники._ДемоКонтрагенты Тогда
		
		Правило = НайтиПРХПоИмени(ПравилаРасчетаХеша, "Справочник_Контрагенты");
		ПравилаДляРасчетаОбъекта.Добавить(Правило);
	
	// У организации 2 правила, считаем по обоим	
	ИначеЕсли ОбъектМетаданных = Метаданные.Справочники._ДемоОрганизации Тогда
		
		Правила = НайтиПРХ(ПравилаРасчетаХеша, ОбъектМетаданных);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПравилаДляРасчетаОбъекта, Правила);
		
	КонецЕсли;
	
КонецПроцедуры

// Расчет хеша в обход правил, по своим алгоритмам
// 
// Параметры:
//  ОбъектОчереди - ЛюбаяСсылка - 
//  ПравилаРасчетаХеша - ТаблицаЗначений - см.рдв_МенеджерОбмена.ИнициализироватьПравилаКонвертации()
//  ТаблицаХешей - ТаблицаЗначений -
// 
//
Процедура ХешРассчитать(ОбъектОчереди, ПравилаРасчетаХеша, ТаблицаХешей) Экспорт
	
	// Специфичная логика, которая не укладывается в правила расчета, например, движения документа.
	
КонецПроцедуры

```

## Итого по модулю `рдв_ФормированиеХешейОбъектовДемо`

- В созданном модуле должны быть реализованы следующие методы:
 ```
Процедура ИнициализироватьПравилаРасчетаХеша(ПравилаРасчетаХеша) Экспорт - создание правила расчета
Процедура ПравилаРасчетаХеша(ОбъектОчереди, ПравилаРасчетаХеша, ПравилаДляРасчетаОбъекта) Экспорт - выбор правил для расчета
Процедура ХешРассчитать(ОбъектОчереди, ПравилаРасчетаХеша, ТаблицаХешей) Экспорт - специфичная логика вне правил
 ```

Подорбнее [о процессах регистрации и формирования хеша](5%20process%20of%20reg%20of%20changes.md)
