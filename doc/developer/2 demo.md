# Описание процесса встройки своих методов

Для реализации прикладной логики для целей интеграции необходимо создать ряд общих модулей которые будут ее реализовывать. Все примеры Демо модулей находятся в конфигурации.

Модули:
- Шаблон `рдв_ИнтеграцияДемо` - предоставляет возможность реализовать новый тип внешней системы
- Шаблон `рдв_МониторингДанныхДемо` - задает параметры для внешней системы мониторинга всего механизмы интеграции
- Шаблон `рдв_МенеджерОбменаДемо` - позволяет определить правила конвертации и модификации объектов для целей выгрузки
- Шаблон `рдв_РаботаСОчередямиДемо` - обработка очередей интеграции
- Шаблон `рдв_РегистрацияИзмененийДемо` - описывает правила регистрации/маршрутизации объектов для последующей передачи во внешние системы
- Шаблон `рдв_ОбработчикиСобытийДемо` - определяет механизмы регистрации объектов
- Шаблон `рдв_ФормированиеХешейОбъектовДемо` - расчет хеша объектов для целей сверки полноты и точности данных.


Созданные модули необходимо прописать в `рдв_ИнтеграцияПереопределяемый`
- имя аналога `рдв_ИнтеграцияДемо` в методе `МодульПрикладнойЛогикиИнтеграции`
- имя аналога `рдв_РаботаСОчередямиДемо` в методе `МодульПрикладнойЛогикиОчередей`
- имя аналога `рдв_ФормированиеХешейОбъектовДемо` в методе `МодульПрикладнойЛогикиФормированияХешей`
- имя аналога `рдв_МенеджерОбменаДемо` в методе `МодульПрикладнойЛогикиМенеджераОбмена`
- имя аналога `рдв_РегистрацияИзмененийДемо` в методе `МодульПрикладнойЛогикиРегистрацииИзменений`
- имя аналога `рдв_ОбработчикиСобытийДемо` в методе `МодульПрикладнойЛогикиОбработчиковСобытий`
- имя аналога `рдв_МониторингДанныхДемо` в методе `МодульПрикладнойЛогикиМониторингаДанных`

Действия выше позволят переопределить прикладную логику решения в соответствующих модулях, не затрагивая основное решение. В каждом созданном модуле необходимо реализовать методы, которые ожидает основное решение. Скопировать все методы Области `ПрограммныйИнтерфейс` Демо модуля шаблона. Ниже будут описаны основные методы каждого модуля, которые необходимо реализовать для запуска.

# `рдв_ИнтеграцияДемо`

Используется для:
 - переопределить существующий тип, ввести дополнительную логику в типовые механизмы
 - добавить новый тип (kafka, ftp)
 В случаях выше создается новый модуль с соответсвующим API. Подбробнее в [новый механизм транспорта](3%202%20new%20transport.md)

# `рдв_МенеджерОбменаДемо`

Используется для:
 - задать правила конвертации объектов интеграции
 - переопределить данные для выгрузки
 - определить идентификатор базы

## Правила конвертации
Согласно указаным правилам будет происходит конвертация объекта в сообщение обмена. В модуле необходимо создать метод `ЗаполнитьПравилаКонвертацииОбъектов`. Подробнее в модуле Демо.

```
#Область МенеджерОбмена

// Позволяет в зависимости от направлений обмена сформировать правила для объектов интеграции.
// 
Процедура ЗаполнитьПравилаКонвертацииОбъектов(СвойстваКонвертации, НаправлениеОбмена) Экспорт

Если НаправлениеОбмена = "Отправка" Тогда

// Пример
ПКО = рдв_МенеджерОбмена.ДобавитьПКО(СвойстваКонвертации, Метаданные.Справочники._ДемоКонтрагенты);
ПКО.ИмяОбъекта = "Справочник.Контрагенты";
ПКО.ИмяПКО = "Справочник_Контрагенты";

КонвертацияШапки = ПКО.КонвертацияШапки;
ИмяРеквизита = "Код";
ИмяКлючаФормата = "Код"; // Представление реквизита в формате обмена
Обязательный = Истина;
рдв_МенеджерОбмена.ДобавитьПКС(КонвертацияШапки, ИмяРеквизита, ИмяКлючаФормата, Обязательный);
...

ИмяТаблЧасти = "КонтактнаяИнформация";
ИмяКлючаФормата = "КонтактнаяИнформация"; // Представлениетреквизита в формате обмена
ТаблЧасть = рдв_МенеджерОбмена.ДобавитьТаблЧасть(СвойстваКонвертации, ПКО, ИмяТаблЧасти, ИмяКлючаФормата);
...

рдв_МенеджерОбмена.ДобавитьПКС(ТаблЧасть, "Представление", "Представление");

ПоляПоиска = ПКО.ПоляПоиска;
рдв_МенеджерОбмена.ДобавитьПоляПоиска(ПоляПоиска, "Код");

КонецПроцедуры

#КонецОбласти
```

## Идентификатор базы
Используется для работы основного механизма и идентификации источника сообщения. Имена на каждого участника интеграции должны быть уникальны.

```
// Для целей формирования сообщения обмена
// добавит имя базы в сообщение обмена
// meta.baseid
Функция ИмяИБ() Экспорт
	Возврат "Узел1";
КонецФункции
```

## Данные для выгрузки
Позволяет изменить входящее/исходящее сообщение. Подробнее в модуле Демо областях программного интерфейса Выгрузка/Загрузка.

# `рдв_РаботаСОчередямиДемо`

Используется для:
 - обработки очередей интеграции: обработка входящих/исходящих сообщений, очереди расчета хеша.


# `рдв_РегистрацияИзмененийДемо`

Используется для:
 - указания объектов участвующих в интеграции
 - указания внешних систем для каждого объекта интеграции
 - переопределения маршрутизации сообщений

Для каждого объекта интеграции необходимо указать правила регистрации, которые будут определять для каких внешних источников регистрировать объект. Один объект может участвовать в неограниченом количестве внешних источников. Подробнее в модуле Демо.
Необходимо добавить метод:

```
#Область РегистрацияИзменений

// Правила регистрации объектов для обмена в интеграции.
// При изменении объекта, будет зарегистрирован в указанной внешней системе
// по указаным параметрам
Процедура ЗаполнитьПравилаРегистрации(ПравилаРегистрации) Экспорт
		
	RMQ = Справочники.рдв_ВнешниеСистемы.Предопределенный("RMQ");
	
	ДобавитьПравилоРегистрации(ПравилаРегистрации, 
								Метаданные.Справочники._ДемоКонтрагенты, 
								RMQ, 
								"RMQ",
								"RMQ",
								"exchange",
								"Контрагенты" - для целей маршрутизации брокера
								);
								
КонецПроцедуры

Процедура ДобавитьПравилоРегистрации(ПравилаРегистрации, 
										МетаданныеОбъекта, 
										ВнешняяСистема,  
										ПредставлениеИнтерфейса = "", 
										АдресМетода = "", 
										ИмяВидаСообщения = "")

	ВидСообщения = Неопределено;
	
	Если ЗначениеЗаполнено(ИмяВидаСообщения) Тогда
		ВидСообщения = Справочники.рдв_ВидыСообщенийИнтеграции.ПолучитьВидСообщения(ИмяВидаСообщения);
	КонецЕсли;
											
	ПравилоРегистрации = ПравилаРегистрации.Добавить();
	ПравилоРегистрации.МетаданныеОбъекта = МетаданныеОбъекта;
	ПравилоРегистрации.ВнешняяСистема = ВнешняяСистема;
	ПравилоРегистрации.АдресМетода = АдресМетода;
	ПравилоРегистрации.ВидСообщения = ВидСообщения;
	
КонецПроцедуры

#КонецОбласти
```

# `рдв_ОбработчикиСобытийДемо`

Используется для:
 - определить механизм регистрации объекта к интеграции. Напрямую в регистр регистрации или через сторонние механизмы, например, механизм очередей.

```
Процедура СправочникиПриЗаписи(Источник, Отказ) Экспорт
	
	рдв_РегистрацияИзменений.ЗарегистрироватьИзменения(Источник);
	
КонецПроцедуры

Процедура ДокументыПриЗаписи(Источник, Отказ) Экспорт
	
	ТипИсточника = ТипЗнч(Источник);
  	Если ТипИсточника = Тип("ДокументОбъект._ДемоЗаказПокупателя") Тогда
	
		рдв_РегистрацияИзменений.ЗарегистрироватьИзменения(Источник);
		
	КонецЕсли;

КонецПроцедуры

Процедура НаборыЗаписейПриЗаписи(Источник, Отказ, Замещение) Экспорт
	
	ТипИсточника = ТипЗнч(Источник);
	Если ТипИсточника = Тип("РегистрСведенийНаборЗаписей.КурсыВалют") Тогда 
	
		рдв_РегистрацияИзменений.ЗарегистрироватьИзменения(Источник);
		
	КонецЕсли;
	
КонецПроцедуры
```

# Контроль целостности данных

Используется для:
 - сверка полноты и точности данных между точками интеграции

Далее инструкция [контроль целостности данных](4%20check%20that%20data%20fine.md)