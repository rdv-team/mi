
#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.ДекорацияРаскрыть.Видимость Тогда
		Возврат;
	КонецЕсли;
	
	СписокПриАктивизацииСтрокиНаСервере(ТекущиеДанные.ИдентификаторСообщения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияРаскрытьНажатие(Элемент)
	
	Элементы.ГруппаДанные.Видимость = Истина;
	Элементы.ДекорацияРаскрыть.Видимость = Ложь;
	
	СписокПриАктивизацииСтроки(Элемент);

КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСвернутьНажатие(Элемент)
	
	Элементы.ГруппаДанные.Видимость = Ложь;
	Элементы.ДекорацияРаскрыть.Видимость = Истина;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтправитьСообщения(Команда)
	
	ОтправитьСообщенияНаСервере();
	
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.рдв_СообщенияИнтеграции"));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОтправитьСообщенияНаСервере()
	
	Если Элементы.Список.ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеВыгрузки = Новый Соответствие;
	ДанныеЧтения = Новый Массив;
	Для Каждого Строка Из Элементы.Список.ВыделенныеСтроки Цикл
		Запись = РегистрыСведений.рдв_СообщенияИнтеграции.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Запись, Строка);
		Запись.Прочитать();
		
		Если Запись.СтатусОбработки = Перечисления.рдв_СтатусыОбработкиСообщений.Обработано Тогда
			Продолжить;
		КонецЕсли;
		Если Запись.Направление = Перечисления.рдв_НаправленияСообщенийИнтеграции.Входящее Тогда
			ДанныеЧтения.Добавить(Строка.ИдентификаторСообщения);
			Продолжить;
		КонецЕсли;
		
		ВнешняяСистема = Запись.ВнешняяСистема;
		МассивИдентификаторов = ДанныеВыгрузки.Получить(ВнешняяСистема);
		Если МассивИдентификаторов = Неопределено Тогда
			МассивИдентификаторов = Новый Массив;
		КонецЕсли;
		МассивИдентификаторов.Добавить(Строка.ИдентификаторСообщения);
		ДанныеВыгрузки.Вставить(ВнешняяСистема, МассивИдентификаторов);
	КонецЦикла;
	
	Для Каждого КлючИЗначение Из ДанныеВыгрузки Цикл
		рдв_Интеграция.ИнтеграцияОтправка(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	
	Для Каждого ИдентификаторСообщения Из ДанныеЧтения Цикл
		рдв_Интеграция.СообщениеОбработатьПолученное(ИдентификаторСообщения);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СписокПриАктивизацииСтрокиНаСервере(ИдентификаторСообщения)
	
	СообщениеАктуальный = РегистрыСведений.рдв_СообщенияИнтеграции.СообщениеАктуальныйКонтекст(ИдентификаторСообщения);
	
	Если СообщениеАктуальный.Количество() = 0 Тогда
		СообщениеДанные = "";
		ОтветДанные = "";
		Возврат;
	КонецЕсли;
	
	Контекст = СообщениеАктуальный.Контекст.Контекст;
	Если Контекст = Неопределено Тогда
		СообщениеДанные = "";
		ОтветДанные = "";
		Возврат;
	КонецЕсли;
	
	Контекст = Контекст.Получить();
	ОтветДанные = Контекст.Ответ;
	Если Не Контекст.Успешно Тогда
		ОтветДанные = Контекст.ТекстОшибки;
	КонецЕсли;
	
	СообщениеДанные = СообщениеАктуальный.Сообщение.ДанныеСообщения.Получить();

КонецПроцедуры

#КонецОбласти
