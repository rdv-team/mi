#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АдресТекущиеНастройки = Неопределено;
	Тип = Неопределено;
	Параметры.Свойство("Тип", Тип);
	
	Если Тип = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеНастройки = ДополнительныеНастройкиПоУмолчанию(Тип);
	ДобавитьРеквизитыНаФорму(ЭтотОбъект, ДополнительныеНастройки);
	Если Параметры.Свойство("АдресТекущиеНастройки", АдресТекущиеНастройки) 
		И Не ПустаяСтрока(АдресТекущиеНастройки) Тогда
		
		ДополнительныеНастройки = ПолучитьИзВременногоХранилища(АдресТекущиеНастройки);
		ОбновитьНастройкиНаФорме(ЭтотОбъект, ДополнительныеНастройки);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	Результат = Неопределено;
	РезультатЗакрытия(Результат);
	Закрыть(Результат);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура РезультатЗакрытия(Результат)
	
	Результат = Новый Структура;
	
	Для Каждого Стр Из Элементы.ГруппаПраво.ПодчиненныеЭлементы Цикл
		
		Результат.Вставить(Стр.Имя, ЭтотОбъект[Стр.Имя]);
		
	КонецЦикла;

	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДополнительныеНастройкиПоУмолчанию(ТипВнешнейСистемы)
	
	МодульИнтеграции = рдв_Интеграция.МодульИнтеграции(ТипВнешнейСистемы);
	ДополнительныеНастройки = МодульИнтеграции.ДинамическиеНастройкиИтеграцииОписание();
	Возврат ДополнительныеНастройки;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ДобавитьРеквизитыНаФорму(Форма, ДополнительныеНастройки)
	
	Элементы = Форма.Элементы;
	
	МассивРеквизитов = Новый Массив;
	Для Каждого Параметр Из ДополнительныеНастройки Цикл
		Реквизит = Новый РеквизитФормы(Параметр.Ключ, Параметр.Значение.Тип, ,, Истина);
		МассивРеквизитов.Добавить(Реквизит);
	КонецЦикла;
	Форма.ИзменитьРеквизиты(МассивРеквизитов);
	
	ГруппаЛево = Элементы.Найти("ГруппаЛево");
	ГруппаПраво = Элементы.Найти("ГруппаПраво");
	Для Каждого Элемент Из ДополнительныеНастройки Цикл
		
		КолонкаИмя = Элементы.Добавить(СтрШаблон("%1_надпись", Элемент.Ключ), Тип("ПолеФормы"), ГруппаЛево);
		КолонкаИмя.Вид = ВидПоляФормы.ПолеНадписи;
		КолонкаИмя.Заголовок = Элемент.Ключ;
		
		КолонкаИмя = Элементы.Добавить(Элемент.Ключ, Тип("ПолеФормы"), ГруппаПраво);
		КолонкаИмя.Вид = ВидПоляФормы.ПолеВвода;
		КолонкаИмя.Доступность = Истина;
		КолонкаИмя.ПутьКДанным = Элемент.Ключ;
		
		РежимПароля = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Элемент.Значение.ДополнительныеПараметры
																, "РежимПароля", Ложь);
		КолонкаИмя.РежимПароля = РежимПароля;
		
		Форма[Элемент.Ключ] = Элемент.Значение.ЗначениеПоУмолчанию;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбновитьНастройкиНаФорме(Форма, ДополнительныеНастройки)
	
	Если ДополнительныеНастройки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Параметр Из ДополнительныеНастройки Цикл
		
		Форма[Параметр.Ключ] = Параметр.Значение;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
