
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность() И ЗначениеЗаполнено(Код) Тогда
		ОписаниеОбъекта = ОписаниеОбъекта();
		НовыйХеш = рдв_ХешиОбъектов.ХешСтандартныйКоллекции(ОписаниеОбъекта);
		Отказ = Не НовыйХеш = ХешСумма;
	ИначеЕсли Не ЗначениеЗаполнено(ХешСумма) Тогда
		ОписаниеОбъекта = ОписаниеОбъекта();
		ХешСумма = рдв_ХешиОбъектов.ХешСтандартныйКоллекции(ОписаниеОбъекта);
	КонецЕсли;
	
	СформироватьНаименование();
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
	
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		ОтборНабораЗаписей.Загрузить(ДанныеЗаполнения.ОтборНабораЗаписей);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

Процедура СформироватьНаименование()
	
	Если Тип = Перечисления.рдв_ТипыВыгружаемыхОбъектов.Произвольный Тогда
		Наименование = ИдентификаторДанных;
	ИначеЕсли Тип = Перечисления.рдв_ТипыВыгружаемыхОбъектов.Ссылка Тогда
		Наименование = СсылкаНаОбъект;
	ИначеЕсли Тип = Перечисления.рдв_ТипыВыгружаемыхОбъектов.ПодтверждениеПолучения Тогда
		Наименование = СтрШаблон("Подтверждение получения %1", ИдентификаторДанных);
	Иначе
		Наименование = "";
		Для Каждого ЭлементОтбора Из ОтборНабораЗаписей Цикл
			Наименование = Наименование + ЭлементОтбора.Имя+": "+ЭлементОтбора.Значение+", ";
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция ОписаниеОбъекта()
	
	ОписаниеОбъекта = Справочники.рдв_ВыгружаемыеОбъекты.ОписаниеОбъекта();
	ЗаполнитьЗначенияСвойств(ОписаниеОбъекта, ЭтотОбъект, , "ХешСумма,ОтборНабораЗаписей");
	ОписаниеОбъекта.ОтборНабораЗаписей = ОтборНабораЗаписей.ВыгрузитьКолонки("Имя,Значение");
	
	Возврат ОписаниеОбъекта;
	
КонецФункции

#КонецЕсли
