#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция СоздатьОбновитьВыгружаемыйОбъект(Источник = Неопределено, ИдентификаторДанных = Неопределено
										, ВыгружаемыйОбъект = Неопределено, Входящий = Ложь) Экспорт
	
	Возврат СоздатьОбновить(Источник, ИдентификаторДанных, ВыгружаемыйОбъект, Входящий);
	
КонецФункции

Функция ВыгружаемыйОбъект(Источник, ИдентификаторДанных = Неопределено) Экспорт
	
	Описание = ОписаниеИсточника(Источник);
	
	Возврат СуществующийОбъект(Описание);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СоздатьОбновить(Источник, ИдентификаторДанных, ВыгружаемыйОбъект = Неопределено, Входящий = Ложь)
	
	Описание = ОписаниеИсточника(Источник, ИдентификаторДанных);
	Если Описание = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ВыгружаемыйОбъект = Неопределено Тогда

		ЗаписьБД = СуществующийОбъект(Описание);
		Если Не ЗаписьБД = Неопределено Тогда
//			ОбновитьЗапись(ЗаписьБД);
			Возврат ЗаписьБД;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВыгружаемыйОбъект = Неопределено Тогда
		НовыйОбъект = СоздатьЭлемент();
	Иначе
		НовыйОбъект = ВыгружаемыйОбъект.ПолучитьОбъект();
		Если НовыйОбъект = Неопределено Тогда
			НовыйОбъект  = СоздатьЭлемент();
			НовыйОбъект.УстановитьСсылкуНового(ВыгружаемыйОбъект);
		КонецЕсли;
	КонецЕсли;
	НовыйОбъект.Заполнить(Описание);
	НовыйОбъект.Входящий = Входящий;
	НовыйОбъект.ДополнительныеСвойства.Вставить(рдв_ХешиОбъектов.ХешСвойствоОтказатьсяОтРасчета(), Истина);
	НовыйОбъект.Записать();
	
	Возврат НовыйОбъект.Ссылка;
	
КонецФункции

Функция СоздатьВыгружаемыйОбъектПодтверждениеПолучения(ИдентификаторСообщения, ЗагружаемыеДанные = Неопределено, ВидСообщения = Неопределено) Экспорт

	Описание = ОписаниеИсточника(ЗагружаемыеДанные, , ИдентификаторСообщения, Перечисления.рдв_ТипыВыгружаемыхОбъектов.ПодтверждениеПолучения, ВидСообщения);
	
	ЗаписьБД = СуществующийОбъект(Описание);
	Если Не ЗаписьБД = Неопределено Тогда
//		ОбновитьЗапись(ЗаписьБД);
		Возврат ЗаписьБД;
	КонецЕсли;
	
	НовыйОбъект = СоздатьЭлемент();
	НовыйОбъект.Заполнить(Описание);
	НовыйОбъект.ДополнительныеСвойства.Вставить(рдв_ХешиОбъектов.ХешСвойствоОтказатьсяОтРасчета(), Истина);
	НовыйОбъект.Записать();
	Возврат НовыйОбъект.Ссылка;
	
КонецФункции

Функция СоздатьВыгружаемыйОбъектДиагностики() Экспорт

	ИдентификаторДанных = "ping";
	ИдентификаторСообщения = Неопределено;
	ЗагружаемыеДанные = Неопределено;
	Описание = ОписаниеИсточника(ЗагружаемыеДанные, ИдентификаторДанных, ИдентификаторСообщения, Перечисления.рдв_ТипыВыгружаемыхОбъектов.Произвольный);
	
	ЗаписьБД = СуществующийОбъект(Описание);
	Если Не ЗаписьБД = Неопределено Тогда
//		ОбновитьЗапись(ЗаписьБД);
		Возврат ЗаписьБД;
	КонецЕсли;
	
	НовыйОбъект = СоздатьЭлемент();
	НовыйОбъект.Заполнить(Описание);
	НовыйОбъект.ДополнительныеСвойства.Вставить(рдв_ХешиОбъектов.ХешСвойствоОтказатьсяОтРасчета(), Истина);
	НовыйОбъект.Записать();
	Возврат НовыйОбъект.Ссылка;
	
КонецФункции


//Функция ОбновитьВыгружаемыйОбъектПодтверждениеПолучения(ИдентификаторСообщения, ЗагружаемыеДанные) Экспорт
//
//	Описание = ОписаниеИсточника(Неопределено, , ИдентификаторСообщения, Перечисления.рдв_ТипыВыгружаемыхОбъектов.ПодтверждениеПолучения);
//	
//	ЗаписьБД = СуществующийОбъект(Описание);
//	
//	Описание = ОписаниеИсточника(ЗагружаемыеДанные, , ИдентификаторСообщения, Перечисления.рдв_ТипыВыгружаемыхОбъектов.ПодтверждениеПолучения);
//	
//	Если ЗаписьБД = Неопределено Тогда
//		ЗаписьБД = СуществующийОбъект(Описание);	
//	КонецЕсли;
//	
//	ЗаписьБДОбъект = ЗаписьБД.ПолучитьОбъект();
//	ЗаписьБДОбъект.Заполнить(Описание);
//	ЗаписьБДОбъект.Записать();
//	Возврат ЗаписьБДОбъект.Ссылка;
//	
//КонецФункции

Функция СуществующийОбъект(Описание)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВыгружаемыеОбъекты.Ссылка
	|ИЗ
	|	Справочник.рдв_ВыгружаемыеОбъекты КАК ВыгружаемыеОбъекты
	|ГДЕ
	|	ВыгружаемыеОбъекты.ХешСумма = &ХешСумма
	|	И ВыгружаемыеОбъекты.Тип = &Тип
	|	И ВыгружаемыеОбъекты.ИдентификаторДанных = &ИдентификаторДанных
	|	И ВыгружаемыеОбъекты.СсылкаНаОбъект = &СсылкаНаОбъект";
	Запрос.УстановитьПараметр("ХешСумма"			, Описание.ХешСумма);
	Запрос.УстановитьПараметр("Тип"					, Описание.Тип);
	Запрос.УстановитьПараметр("ИдентификаторДанных"	, Описание.ИдентификаторДанных);
	Запрос.УстановитьПараметр("СсылкаНаОбъект"		, Описание.СсылкаНаОбъект);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.Ссылка;
	
КонецФункции

Функция ОписаниеОбъекта() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ХешСумма", "");
	Результат.Вставить("Тип", Перечисления.рдв_ТипыВыгружаемыхОбъектов.ПустаяСсылка());
	Результат.Вставить("СсылкаНаОбъект", Неопределено);
	Результат.Вставить("ИдентификаторДанных", "");
	Результат.Вставить("ИдентификаторСообщения", "");
	Результат.Вставить("ВидСообщения", "");
	ТЗ = Новый ТаблицаЗначений();
	ТЗ.Колонки.Добавить("Имя", ОбщегоНазначения.ОписаниеТипаСтрока(100));
	ТЗ.Колонки.Добавить("Значение");
	Результат.Вставить("ОтборНабораЗаписей", ТЗ);
	
	Возврат Результат;
	
КонецФункции

Функция ОписаниеИсточника(Источник = Неопределено, ИдентификаторДанных = Неопределено,
									ИдентификаторСообщения = Неопределено, Тип = Неопределено,
									ВидСообщения = Неопределено)
	
	Описание = ОписаниеОбъекта();
	
	Если Источник = Неопределено И ИдентификаторСообщения = Неопределено Тогда
		
		Если Не ЗначениеЗаполнено(ИдентификаторДанных) Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Описание.Тип = Перечисления.рдв_ТипыВыгружаемыхОбъектов.Произвольный;
		
		Описание.ИдентификаторДанных = ИдентификаторДанных;
		
	ИначеЕсли Источник = Неопределено И НЕ ИдентификаторСообщения = Неопределено Тогда
		Описание.ИдентификаторСообщения = ИдентификаторСообщения;
	Иначе
		МетаданныеИсточника = Источник.Метаданные();
		ИмяМетаданных = МетаданныеИсточника.ПолноеИмя();
		Если Лев(ИмяМетаданных, 7) = "Регистр" Тогда
			
			Если НЕ МетаданныеИсточника = Метаданные.РегистрыСведений.рдв_СообщенияИнтеграции Тогда
				Описание.Тип = Перечисления.рдв_ТипыВыгружаемыхОбъектов.НаборЗаписей;
				Описание.ИдентификаторДанных = ИмяМетаданных;
				Для Каждого ЭлементОтбора Из Источник.Отбор Цикл
					НоваяСтрока = Описание.ОтборНабораЗаписей.Добавить();
					НоваяСтрока.Имя = ЭлементОтбора.Имя;
					НоваяСтрока.Значение = ЭлементОтбора.Значение;
				КонецЦикла; 
			КонецЕсли;
			
		Иначе
			
			Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Источник)) Тогда
				СсылкаНаОбъект = Источник;
			Иначе
				СсылкаНаОбъект = Источник.Ссылка;
			КонецЕсли;
			
			Описание.Тип = Перечисления.рдв_ТипыВыгружаемыхОбъектов.Ссылка;
			Описание.ИдентификаторДанных = ИмяМетаданных;
			Описание.СсылкаНаОбъект = СсылкаНаОбъект;
			
		КонецЕсли;
	КонецЕсли;
	
	Если Не Тип = Неопределено Тогда
		Описание.Тип = Тип;
	КонецЕсли;
	
	Описание.ИдентификаторСообщения = ИдентификаторСообщения;
	Описание.ВидСообщения = ВидСообщения;
	Хеш = рдв_ХешиОбъектов.ХешСтандартныйКоллекции(Описание);
	Описание.ХешСумма = Хеш;
	
	Возврат Описание;
	
КонецФункции

Процедура ОбновитьЗапись(ЗаписьБД)
	
	рдв_ХешиОбъектов.ХешСтандартныйОбъекта(ЗаписьБД,, Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
