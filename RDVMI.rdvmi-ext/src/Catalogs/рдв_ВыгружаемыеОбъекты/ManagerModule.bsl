Функция СоздатьВыгружаемыйОбъект(Источник = Неопределено, ИдентификаторДанных = Неопределено) Экспорт
	
	Если Источник = Неопределено Тогда
		Если Не ЗначениеЗаполнено(ИдентификаторДанных) Тогда
			Возврат Неопределено;
		КонецЕсли;
		Возврат СоздатьПроизвольныйВыгружаемыйОбъект(ИдентификаторДанных);
	Иначе
		МетаданныеИсточника = Источник.Метаданные();
		ИмяМетаданных = МетаданныеИсточника.ПолноеИмя();
		Если Лев(ИмяМетаданных, 7) = "Регистр" Тогда
			Возврат СоздатьВыгружаемыйОбъектРегистр(Источник, ИмяМетаданных);
		Иначе
			Возврат СоздатьВыгружаемыйОбъектСсылка(Источник, ИмяМетаданных);
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Функция СоздатьПроизвольныйВыгружаемыйОбъект(ИдентификаторДанных)

	НовыйОбъект = Справочники.рдв_ВыгружаемыеОбъекты.СоздатьЭлемент();
	НовыйОбъект.Тип = Перечисления.рдв_ТипыВыгружаемыхОбъектов.Произвольный;
	НовыйОбъект.ИдентификаторДанных = ИдентификаторДанных;
	НовыйОбъект.Записать();
	Возврат НовыйОбъект.Ссылка;
	
КонецФункции

Функция СоздатьВыгружаемыйОбъектРегистр(Источник, ИдентификаторДанных)
	
	НовыйОбъект = Справочники.рдв_ВыгружаемыеОбъекты.СоздатьЭлемент();
	НовыйОбъект.Тип = Перечисления.рдв_ТипыВыгружаемыхОбъектов.НаборЗаписей;
	НовыйОбъект.ИдентификаторДанных = ИдентификаторДанных;
	Для Каждого ЭлементОтбора Из Источник.Отбор Цикл
		НоваяСтрока = НовыйОбъект.ОтборНабораЗаписей.Добавить();
		НоваяСтрока.Имя = ЭлементОтбора.Имя;
		НоваяСтрока.Значение = ЭлементОтбора.Значение;
	КонецЦикла;
	НовыйОбъект.Записать();
	Возврат НовыйОбъект.Ссылка;
	
КонецФункции

Функция СоздатьВыгружаемыйОбъектСсылка(Источник, ИдентификаторДанных)
	
	НовыйОбъект = Справочники.рдв_ВыгружаемыеОбъекты.СоздатьЭлемент();
	НовыйОбъект.Тип = Перечисления.рдв_ТипыВыгружаемыхОбъектов.Ссылка;
	НовыйОбъект.ИдентификаторДанных = ИдентификаторДанных;
	НовыйОбъект.СсылкаНаОбъект = Источник.Ссылка;
	НовыйОбъект.Записать();
	Возврат НовыйОбъект.Ссылка;
	
КонецФункции
