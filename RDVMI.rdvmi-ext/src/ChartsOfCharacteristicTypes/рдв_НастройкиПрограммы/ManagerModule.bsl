
#Область ПрограммныйИнтерфейс

// Возвращает значение общей настройки программы.
//
// Параметры:
//  Настройка                - Строка, ПланВидовХарактеристикСсылка.рдв_НастройкиПрограммы
//                                    - имя предопределенного значения настройки или ссылка на настройку (ПВХ).
//  ПроверятьЗаполнение      - Булево - 
//  ОбязательноеЗаполнение   - Булево - 
// 
// Возвращаемое значение:
//  Характеристика.рдв_НастройкиПрограммы - значение настройки
//
Функция ЗначениеНастройки(Знач Настройка, ПроверятьЗаполнение = Истина, ОбязательноеЗаполнение = Ложь) Экспорт
	
	Если ТипЗнч(Настройка) = Тип("Строка") Тогда
		Настройка = ПланыВидовХарактеристик.рдв_НастройкиПрограммы[Настройка];
	КонецЕсли;
	
	ЗначениеНастройки = Настройка.Значение;
	
	Если Настройка.ВидЗначения = Перечисления.рдв_ВидыЗначенийНастроек.Соответствие Тогда
		// Проверка на заполненность соответствия
		
	ИначеЕсли Настройка.ВидЗначения = Перечисления.рдв_ВидыЗначенийНастроек.СписокЗначений Тогда
		// Проверка на заполненность соответствия
		
	Иначе
		// Проверка на заполненность значения
		Если Не ЗначениеЗаполнено(ЗначениеНастройки) И (ОбязательноеЗаполнение Или ПроверятьЗаполнение) Тогда
			ТекстСообщения = НСтр("ru = 'Необходимо заполнить значение настройки ""[Настройка]"".'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "[Настройка]", Формат(Настройка));
			Если ОбязательноеЗаполнение Тогда
				ВызватьИсключение ТекстСообщения; 
			Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Настройка);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Настройка.ВидЗначения = Перечисления.рдв_ВидыЗначенийНастроек.Соответствие Тогда
		Возврат ТаблицаСоответствийВСоответствие(Настройка.СоответствиеЗначений, Настройка);
	ИначеЕсли Настройка.ВидЗначения = Перечисления.рдв_ВидыЗначенийНастроек.СписокЗначений Тогда
		Возврат ТаблицаЗначенийВМассив(Настройка.Значения, Настройка);
	Иначе
		Возврат Настройка.ТипЗначения.ПривестиЗначение(ЗначениеНастройки);
	КонецЕсли;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцеудурыИФункции

Функция ТаблицаСоответствийВСоответствие(ТаблицаСоответствиеЗначений, Настройка)
	
	СоответствиеЗначений = Новый Соответствие;
	
	Для каждого СтрокаТЧ Из ТаблицаСоответствиеЗначений Цикл
		СоответствиеЗначений.Вставить(СтрокаТЧ.Ключ, Настройка.ТипЗначения.ПривестиЗначение(СтрокаТЧ.Значение));
	КонецЦикла;
	
	Возврат СоответствиеЗначений;
	
КонецФункции

Функция ТаблицаЗначенийВМассив(ТаблицаЗначений, Настройка)
	
	МассивЗначений = Новый Массив;
	
	Для Каждого СтрокаТЧ Из ТаблицаЗначений Цикл
		МассивЗначений.Добавить(Настройка.ТипЗначения.ПривестиЗначение(СтрокаТЧ.Значение));
	КонецЦикла;
	
	Возврат МассивЗначений;
	
КонецФункции

#КонецОбласти 