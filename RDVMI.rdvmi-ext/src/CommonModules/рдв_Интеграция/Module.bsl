
Процедура РегламентЗапускаИнтеграции(Знач ВнешняяСистема) Экспорт
	
	Если Не ЗначениеЗаполнено(ВнешняяСистема) Тогда
		Возврат;
	КонецЕсли;
	
	ВнешняяСистемаСсылка = Справочники.рдв_ВнешниеСистемы.ВнешняяСистемаПоИмени(ВнешняяСистема);
	
	Если ТипЗнч(ВнешняяСистемаСсылка) <> Тип("СправочникСсылка.рдв_ВнешниеСистемы") Тогда
		Возврат;
	КонецЕсли;
	
	Интеграция(ВнешняяСистемаСсылка);
	
КонецПроцедуры

Процедура Интеграция(ВнешняяСистема, ИдентификаторСообщения = Неопределено) Экспорт

	ТипВнещнейСистемы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВнешняяСистема, "Тип");
	Если ТипВнещнейСистемы = ПредопределенноеЗначение("Перечисление.рдв_ТипыВнешнихСистем.HTTPСервис") Тогда
		ИнтеграцияHTTPСервис(ВнешняяСистема);
	КонецЕсли;
	
КонецПроцедуры


Процедура ИнтеграцияHTTPСервис(ВнешняяСистема, ИдентификаторСообщения = Неопределено) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	рдв_РегистрацияИзменений.ИдентификаторСообщения КАК ИдентификаторСообщения,
	|	рдв_РегистрацияИзменений.ВыгружаемыйОбъект КАК ВыгружаемыйОбъект,
	|	рдв_РегистрацияИзменений.ВнешняяСистема КАК ВнешняяСистема,
	|	рдв_РегистрацияИзменений.АдресМетода КАК АдресМетода,
	|	рдв_РегистрацияИзменений.Интерфейс КАК Интерфейс,
	|	рдв_РегистрацияИзменений.ВидСообщения КАК ВидСообщения,
	|	рдв_РегистрацияИзменений.ВидСообщения.ИмяВидаСообщения КАК ИмяВидаЗапроса,
	|	рдв_РегистрацияИзменений.Интерфейс.ИмяИнтерфейса КАК ИмяИнтерфейса,
	|	рдв_РегистрацияИзменений.Интерфейс.Наименование КАК ПредставлениеИнтерфейса,
	|	рдв_РегистрацияИзменений.ВыгружаемыйОбъект.СсылкаНаОбъект КАК СсылкаНаОбъект
	|ИЗ
	|	РегистрСведений.рдв_РегистрацияИзменений КАК рдв_РегистрацияИзменений
	|ГДЕ
	|	рдв_РегистрацияИзменений.ВнешняяСистема = &ВнешняяСистема
	|	И &УсловиеПоИдентификатору
	|
	|УПОРЯДОЧИТЬ ПО
	|	рдв_РегистрацияИзменений.ДатаРегистрацииВМиллисекундах";
	
	Если Не ЗначениеЗаполнено(ИдентификаторСообщения) Тогда
		ТекстУсловия = "ИСТИНА";
	ИначеЕсли ТипЗнч(ИдентификаторСообщения) = Тип("Массив") Тогда
		ТекстУсловия = "рдв_РегистрацияИзменений.ИдентификаторСообщения В (&ИдентификаторСообщения)";
	Иначе
		ТекстУсловия = "рдв_РегистрацияИзменений.ИдентификаторСообщения = &ИдентификаторСообщения";
	КонецЕсли;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоИдентификатору", ТекстУсловия);
	
	Запрос.УстановитьПараметр("ВнешняяСистема", ВнешняяСистема);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ПараметрыСообщения = Новый Структура;
		ПараметрыСообщения.Вставить("ИмяВидаЗапроса", Выборка.ИмяВидаЗапроса);
		ПараметрыСообщения.Вставить("ИмяИнтерфейса", Выборка.ИмяИнтерфейса);
		ПараметрыСообщения.Вставить("ПредставлениеИнтерфейса", Выборка.ПредставлениеИнтерфейса);
		
		МассивОбъектов = Новый Массив;
		Если ЗначениеЗаполнено(Выборка.СсылкаНаОбъект) Тогда
			МассивОбъектов.Добавить(Выборка.СсылкаНаОбъект);
		КонецЕсли;
		ПараметрыСообщения.Вставить("МассивОбъектов", МассивОбъектов);
		ПараметрыСообщения.Вставить("АдресМетода", Выборка.АдресМетода);
		ПараметрыСообщения.Вставить("ВнешняяСистема", Выборка.ВнешняяСистема);
		
		ПараметрыОбъекта = Новый Структура;
		ПараметрыОбъекта.Вставить("ИдентификаторСообщения", Выборка.ИдентификаторСообщения);
		ПараметрыСообщения.Вставить("ТекстСообщения", рдв_ОбменДанными.ОбъектИБВСообщениеОбмена(Выборка.ВыгружаемыйОбъект, ПараметрыОбъекта));
		
		Результат = рдв_РаботаССервисами.ВыгрузитьСообщение(ПараметрыСообщения);
		
		Если Результат.ОшибкаТранспорта Тогда
			Ошибка = Перечисления.рдв_СтатусыРегистрацииИзменений.Ошибка;
			РегистрыСведений.рдв_РегистрацияИзменений.ОбновитьСтатусРегистрации(Выборка.ИдентификаторСообщения, Ошибка, Результат.СообщениеТранспорта);
		Иначе
			РегистрыСведений.рдв_РегистрацияИзменений.УдалитьРегистрацию(Выборка.ИдентификаторСообщения, Выборка.ВыгружаемыйОбъект);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
