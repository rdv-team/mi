// Обработчик подписки на событие рдв_РегистрацияИзменений
//
Процедура рдв_РегистрацияИзмененийПриЗаписи(Источник, Отказ) Экспорт

    ЗарегистрироватьИзменения(Источник);
	
КонецПроцедуры

// Обработчик подписки на событие рдв_РегистрацияИзмененийНабораЗаписей
//
Процедура рдв_РегистрацияИзмененийНабораЗаписейПриЗаписи(Источник, Отказ, Замещение) Экспорт

    ЗарегистрироватьИзменения(Источник);

КонецПроцедуры

// Регистрирует изменения объекта к обмену.
//
// Параметры:
//  Источник - СправочникОбъект, ДокументОбъект, НаборЗаписейОбъект - Объект, регистрируемый к обмену.
//  Отказ - Булево - Отказ записи объекта.
//
Процедура ЗарегистрироватьИзменения(Источник, ИдентификаторСобытия = Неопределено) Экспорт

	ПравилаРегистрации = рдв_РегистрацияИмененийПовтИсп.ИнициализироватьПравилаРегистрации();
	МетаданныеИсточника = Источник.Метаданные();
	СтрокиПравил = ПравилаРегистрации.НайтиСтроки(Новый Структура("МетаданныеОбъекта", МетаданныеИсточника));
	Для Каждого ПравилоРегистрации Из СтрокиПравил Цикл
		ВыгружаемыйОбъект = Справочники.рдв_ВыгружаемыеОбъекты.СоздатьВыгружаемыйОбъект(Источник);
		РегистрыСведений.рдв_РегистрацияИзменений.ЗарегистрироватьИзменениеОбъекта(ВыгружаемыйОбъект, ПравилоРегистрации, ИдентификаторСобытия);
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗарегистрироватьПодтверждениеПолучения(ВнешняяСистема, АдресМетода, ВходящееСообщение, Результат) Экспорт

	СтруктураДанных = Результат.СтруктураДанных;
	Если Не СтруктураДанных.Свойство("meta") Тогда
		Возврат;
	КонецЕсли;
	
	МетаданныеСообщения = СтруктураДанных.meta;
	ИдентификаторСообщения = МетаданныеСообщения.id;
	ИдентификаторСобытия = МетаданныеСообщения.eventid;
	Получатель = МетаданныеСообщения.baseid;

	ВыгружаемыйОбъект = Справочники.рдв_ВыгружаемыеОбъекты.СоздатьВыгружаемыйОбъектПодтверждениеПолучения(ИдентификаторСообщения, ИдентификаторСобытия, Результат.Успех, Результат.ТекстОшибки);
	
	Интерфейс = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВходящееСообщение, "Интерфейс");
	
	ИмяВидаСообщения = Получатель+"log";
	ВидСообщения = Справочники.рдв_ВидыСообщенийИнтеграции.ПолучитьВидСообщения(Интерфейс, ИмяВидаСообщения);
	
	ПравилоРегистрации = Новый Структура;
	ПравилоРегистрации.Вставить("Интерфейс", Интерфейс);
	ПравилоРегистрации.Вставить("ВнешняяСистема", ВнешняяСистема);
	ПравилоРегистрации.Вставить("АдресМетода", АдресМетода);
	ПравилоРегистрации.Вставить("ВидСообщения", ВидСообщения);
	РегистрыСведений.рдв_РегистрацияИзменений.ЗарегистрироватьИзменениеОбъекта(ВыгружаемыйОбъект, ПравилоРегистрации, ИдентификаторСобытия);
	
КонецПроцедуры

