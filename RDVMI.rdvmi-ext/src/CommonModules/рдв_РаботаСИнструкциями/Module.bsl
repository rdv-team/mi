
#Область ПрограммныйИнтерфейс

Функция АдресПространстваFAQ() Экспорт 
	
	// VELO-1570 05.02.2024 pkharchuk@rdv.team
	
	ЗначениеНастройки = "";
	
	рдв_РаботаСИнструкциямиПереопределяемый
	    .АдресПространстваFAQ_ПриОпределении(ЗначениеНастройки);
	
	Если Не ЗначениеЗаполнено(ЗначениеНастройки) Тогда
		ВызватьИсключение "Не заполнен адрес пространсва FAQ";
	КонецЕсли;
	
	Возврат ЗначениеНастройки;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// рдв_СобытияФорм

Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	// VELO-1570 05.02.2024 pkharchuk@rdv.team
	
	ДобавитьКомандуПоказатьИнструкциюНаПортале(Форма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

// Конец рдв_СобытияФорм

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьКомандуПоказатьИнструкциюНаПортале(Форма, Отказ, СтандартнаяОбработка)
	
	// VELO-1570 05.02.2024 pkharchuk@rdv.team
	
	МассивПодстрок = СтрРазделить(Форма.ИмяФормы, ".");
	Если МассивПодстрок.Количество() < 2 Тогда
		Возврат;
	КонецЕсли;
	
	ЭлементСправка = Форма.Элементы.Найти("ФормаСправка");
	Если ЭлементСправка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СоответствиеОбъектовМетаданныхИнструкций = рдв_РаботаСИнструкциямиПовтИсп
		.ПолучитьСоответствиеОбъектовМетаданныхИнструкций();
	
	ИмяМетаданных = СтрШаблон("%1.%2", МассивПодстрок[0], МассивПодстрок[1]);
	Если СоответствиеОбъектовМетаданныхИнструкций.Получить(ИмяМетаданных) <> Неопределено Тогда
		Элемент = рдв_РаботаСФормами.ДобавитьКоманду(Форма,
			"рдв_ПоказатьИнструкциюНаПортале",
			"Инструкции",
			"Подключаемый_ВыполнитьКоманду",,,
			БиблиотекаКартинок.ВажнаяИнформация32,
			ЭлементСправка.Родитель,
			ЭлементСправка
		);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

