
#Область ПрограммныйИнтерфейс

Функция ПодключитьКомпонентуИзМакета(Знач ИмяМакета, Знач ИмяКомпоненты) Экспорт
	
	Если СтрНайти(ИмяМакета, ".") = 0 Тогда
		ИмяМакета = "ОбщийМакет." + ИмяМакета;
	КонецЕсли;
	
	ИмяПодключения = ИмяКомпоненты + "_NativeComponent";
	
	Успех = ПодключитьВнешнююКомпоненту(ИмяМакета, ИмяПодключения, ТипВнешнейКомпоненты.Native);
	Если Не Успех Тогда
		ТекстОшибки = НСтр("ru = 'Ошибка подключения внешней компоненты %1 из общего макета %2'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, ИмяКомпоненты, ИмяМакета);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	ИмяТипаКомпоненты = "AddIn.%1.%2";
	ИмяТипаКомпоненты = СтрШаблон(ИмяТипаКомпоненты, ИмяПодключения, ИмяКомпоненты);
	
	Компонента = Новый(ИмяТипаКомпоненты);
	Если Компонента = Неопределено Тогда
		ТекстОшибки = НСтр("ru = 'Ошибка создания объекта внешней компоненты ""%1"" из макета ""%2""'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, ИмяКомпоненты, ИмяМакета);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;

	Возврат Компонента;
	
КонецФункции

#КонецОбласти
