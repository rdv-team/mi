
#Область ПрограммныйИнтерфейс

Процедура Пауза(Знач Секунд) Экспорт
	
	Секунд = Окр(Секунд);
	Если Секунд < 1 Тогда
		ТекстОшибки = НСтр("ru = 'Количество секунд паузы должно быть больше или равно 1.'");
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
	// Для контроля времени выполнения паузы.
	НачалоПаузы = ТекущаяУниверсальнаяДатаВМиллисекундах();
	
	КомпонентаПауза = рдв_ПаузаПовтИсп.ПодключитьКомпонентуИзМакета("рдв_КомпонентаПауза", "Sleep");

	Попытка
		КомпонентаПауза.ПаузаСек(Секунд);
	Исключение
		ОписаниеОшибки = КомпонентаПауза.ОписаниеПоследнейОшибки();
		Если Не ПустаяСтрока(ОписаниеОшибки) Тогда
			ТекстОшибки = НСтр("ru = 'Ошибка компоненты: %1'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, ОписаниеОшибки);
			ВызватьИсключение ТекстОшибки;
		Иначе
			ВызватьИсключение;			
		КонецЕсли;
	КонецПопытки;
	
	КонецПаузы = ТекущаяУниверсальнаяДатаВМиллисекундах();
	
	// Контроль паузы.
	ОтклонениеМс = (КонецПаузы - НачалоПаузы) - Секунд * 1000;
	ОтклонениеМс = Макс(ОтклонениеМс, -ОтклонениеМс);
	
	//++ VELO-1910 02.04.2024 dvasilev@rdv-it.com
	//ОтклонениеПроц = Окр(1000 * ОтклонениеМс / (Секунд * 1000));
	ОтклонениеПроц = Окр(100 * ОтклонениеМс / (Секунд * 1000));
	//-- VELO-1910 02.04.2024 dvasilev@rdv-it.com
	
	МаксОтклонениеПроц = 10;
	МаксОтклонениеМс = 100;
	
	Если ОтклонениеМс > МаксОтклонениеМс
		И ОтклонениеПроц > МаксОтклонениеПроц Тогда
		ТекстОшибки = НСтр("ru = 'Большое отклонение времени паузы (%1 сек):
                            |- отклонение %2 мс
                            |- процент отклонения %3%%'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, Секунд, ОтклонениеМс, ОтклонениеПроц);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти