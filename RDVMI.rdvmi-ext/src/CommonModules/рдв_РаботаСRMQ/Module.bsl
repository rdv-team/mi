Функция ИнициализироватьКлиентПодключения(ВнешняяСистема, ВыборкаЗапроса) Экспорт

	ПараметрыПодключения = рдв_РаботаСRMQ.ПустыеПараметрыПодключения();

	ДанныеАвторизации = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(ВнешняяСистема, "Пользователь, Пароль");
	ПараметрыПодключения.Пароль = ?(ЗначениеЗаполнено(ДанныеАвторизации.Пароль), ДанныеАвторизации.Пароль, "");
	ПараметрыПодключения.Пользователь = ?(ЗначениеЗаполнено(ДанныеАвторизации.Пользователь), ДанныеАвторизации.Пользователь, "");

	ЗаполнитьЗначенияСвойств(ПараметрыПодключения, ВыборкаЗапроса);
	Клиент = СоздатьКлиентПодключения(ПараметрыПодключения);

	Возврат Клиент;
	
КонецФункции

Функция СоздатьКлиентПодключения(ПараметрыПодключения) Экспорт
		
	Компонента = Обработки.рдв_КомпонентаRabbitMQ.Создать();
	ЗаполнитьЗначенияСвойств(Компонента, ПараметрыПодключения);
	Компонента.УстановитьСоединение();
	
	Возврат Компонента;

КонецФункции

Функция ПустыеПараметрыПодключения() Экспорт
	Структура = Новый Структура();
	Структура.Вставить("Адрес");
	Структура.Вставить("Пользователь");
	Структура.Вставить("Пароль");
	Структура.Вставить("ВиртуальныйХост");
	Структура.Вставить("Порт");
	Структура.Вставить("ЧастотаПульса");
	Структура.Вставить("ТочкаОбмена");
	Структура.Вставить("ЗащищенноеСоединение");
	
	Возврат Структура;
КонецФункции

Функция ДанныеЗапроса() Экспорт

	Структура = Новый Структура;
	Структура.Вставить("БазовыйURL");
	Структура.Вставить("ОтносительныйURL");
	Структура.Вставить("Заголовки", Новый Массив);
	Структура.Вставить("Запрос");
	Структура.Вставить("ДатаСообщения");
	Структура.Вставить("ДатаСообщенияМС");
	Структура.Вставить("ЗащищенноеСоединение");
	Структура.Вставить("ИдентификаторСообщения");
	Структура.Вставить("ИдентификаторСобытия");
	Структура.Вставить("Пароль");
	Структура.Вставить("Пользователь");
	Структура.Вставить("Порт");
	Структура.Вставить("Сервер");
	Структура.Вставить("Таймаут");
	Структура.Вставить("Тело");
	Возврат Структура;
	
КонецФункции

Функция ЗаполнитьДанныеЗапросаОтправка(ПараметрыСообщения) Экспорт
	
	ДанныеЗапроса = ДанныеЗапроса();
	ЗаполнитьЗначенияСвойств(ДанныеЗапроса, ПараметрыСообщения);
	ДанныеЗапроса.Вставить("БазовыйURL", ПараметрыСообщения.Сервер+"/"+ПараметрыСообщения.АдресСервиса);
	ДанныеЗапроса.Вставить("ОтносительныйURL", ПараметрыСообщения.АдресМетода);
	ДанныеЗапроса.Вставить("ДатаСообщения", ТекущаяДата());
	ДанныеЗапроса.Вставить("ДатаСообщенияМс", ТекущаяУниверсальнаяДатаВМиллисекундах());
	ДанныеЗапроса.Вставить("Тело", ПараметрыСообщения.ТекстСообщения);
	ДанныеЗапроса.Вставить("ВнешняяСистема", ПараметрыСообщения.ВнешняяСистема);
	Возврат ДанныеЗапроса;
	
КонецФункции

Функция ЗаполнитьДанныеЗапросаПолучение(Выборка, ПараметрыПодключения, Результат) Экспорт
	
	ДанныеЗапроса = ДанныеЗапроса();
	ЗаполнитьЗначенияСвойств(ДанныеЗапроса, Выборка);
	ЗаполнитьЗначенияСвойств(ДанныеЗапроса, ПараметрыПодключения);
	ДанныеЗапроса.Вставить("ДатаСообщения", ТекущаяДата());
	ДанныеЗапроса.Вставить("ДатаСообщенияМс", ТекущаяУниверсальнаяДатаВМиллисекундах());
	ДанныеЗапроса.Вставить("ИдентификаторСообщения", Результат.СвойстваСообщения.MessageID);
	ДанныеЗапроса.Вставить("ИдентификаторСобытия", Результат.СвойстваСообщения.AppID);
	Возврат ДанныеЗапроса;
	
КонецФункции

Функция ДанныеОтвета() Экспорт

	Структура = Новый Структура;
	Структура.Вставить("ДанныеТиповогоОтвета");
	Структура.Вставить("ДатаОтвета");
	Структура.Вставить("ДатаОтветаМС");
	Структура.Вставить("Заголовки", Новый Массив);
	Структура.Вставить("Ответ");
	Структура.Вставить("КодСостоянияОтвета");
	Структура.Вставить("ОшибкаТранспорта", Ложь);
	Структура.Вставить("ОшибкиТиповогоОтветаСтрокой");
	Структура.Вставить("СообщениеТранспорта");
	Возврат Структура;
	
КонецФункции

Функция ЗаполнитьДанныеОтветаОтправка(Ответ) Экспорт
	
	ДанныеОтвета = ДанныеОтвета();
	ДанныеОтвета.Вставить("ДатаОтвета", ТекущаяДата());
	ДанныеОтвета.Вставить("ДатаОтветаМС", ТекущаяУниверсальнаяДатаВМиллисекундах());
	ДанныеОтвета.ОшибкаТранспорта = Не Ответ.Успешно;
	ДанныеОтвета.ОшибкиТиповогоОтветаСтрокой = Ответ.ТекстОшибки;
	
	Возврат ДанныеОтвета;
	
КонецФункции

Функция ЗаполнитьДанныеОтветаПолучение(Ответ) Экспорт
	
	ДанныеОтвета = ДанныеОтвета();
	ДанныеОтвета.Вставить("ДатаОтвета", ТекущаяДата());
	ДанныеОтвета.Вставить("ДатаОтветаМС", ТекущаяУниверсальнаяДатаВМиллисекундах());
	
	Возврат ДанныеОтвета;
	
КонецФункции

Функция ДанныеСервиса() Экспорт

	Структура = Новый Структура;
	Структура.Вставить("ИдентификаторСообщения");
	Структура.Вставить("ВидСообщения");
	Структура.Вставить("Интерфейс");
	Структура.Вставить("Направление");
	Структура.Вставить("ОбъектыСообщения", Новый Массив);
	Структура.Вставить("Сообщение");
	Структура.Вставить("СтандартнаяСтруктураЗапроса");
	Возврат Структура;
	
КонецФункции

Функция ПараметрыСервиса()
	
	Структура = Новый Структура;
	Структура.Вставить("ИмяИнтерфейса");
	Структура.Вставить("ПредставлениеИнтерфейса");
	Структура.Вставить("СтандартнаяСтруктураЗапроса", Истина);
	Возврат Структура;
	
КонецФункции

Функция ЗаполнитьДанныеСервисаОтправка(ПараметрыСообщения) Экспорт
	
	ДанныеСервиса = ДанныеСервиса();
	ЗаполнитьЗначенияСвойств(ДанныеСервиса, ПараметрыСообщения);
	ДанныеСервиса.Вставить("Направление", Перечисления.рдв_НаправленияСообщенийИнтеграции.Исходящее);
	ДанныеСервиса.Вставить("ОбъектыСообщения", ПараметрыСообщения.МассивОбъектов);
	
	Возврат ДанныеСервиса;
	
КонецФункции

Функция ЗаполнитьДанныеСервисаПолучение(Результат) Экспорт
	
	ДанныеСервиса = ДанныеСервиса();
	ДанныеСервиса.Вставить("ИмяВидаСообщения", Результат.КлючМаршрутизации);
	ДанныеСервиса.Вставить("Направление", Перечисления.рдв_НаправленияСообщенийИнтеграции.Входящее);
	ДанныеСервиса.Вставить("ИдентификаторСообщений", Результат.СвойстваСообщения.MessageId);
	ДанныеСервиса.Вставить("ПараметрыСервиса", рдв_РаботаСRMQПереопределяемый.ПараметрыСервиса());
	
	Возврат ДанныеСервиса;
	
КонецФункции

Функция ВыгрузитьСообщение(Клиент, ПараметрыСообщения) Экспорт
	
	Результат = Клиент.ОтправитьСообщение(ПараметрыСообщения.ТекстСообщения, 
											ПараметрыСообщения.ИмяВидаСообщения, 
											ПараметрыСообщения.ИдентификаторСообщения, 
											ПараметрыСообщения.ИдентификаторСобытия); 
											
	ДанныеОтвета = ЗаполнитьДанныеОтветаОтправка(Результат);											
	ДанныеЗапроса = ЗаполнитьДанныеЗапросаОтправка(ПараметрыСообщения);
	ДанныеСервиса = ЗаполнитьДанныеСервисаОтправка(ПараметрыСообщения);

	Попытка
		рдв_РаботаССервисами.ЗаписатьСообщениеИнтеграции(ДанныеЗапроса, ДанныеОтвета, ДанныеСервиса);
	Исключение
		Клиент.ЗавершитьСоединение();
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат ДанныеОтвета;

КонецФункции

