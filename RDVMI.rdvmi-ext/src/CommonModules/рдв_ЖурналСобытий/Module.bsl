
Процедура рдв_ЖурналСобытийПриЗаписиОчередиПриЗаписи(Источник, Отказ, Замещение) Экспорт
	Для Каждого Запись Из Источник Цикл
		ВестиЖурналСобытий = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись.ТипОчереди, "ВестиЖурналСобытий");
		Если ВестиЖурналСобытий И Запись.Состояние = Перечисления.рдв_СостоянияОчередейОбработкиДанных.Новая Тогда
			Событие = Перечисления.рдв_События.ДобавленВОчередь;
			РегистрыСведений.рдв_ЖурналСобытий.ЗарегистрироватьСобытие(Запись.ИдентификаторЗаписи,,,Запись.Объект,Событие,, Запись.ДатаСоздания);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура рдв_ЖурналСобытийПриЗаписиРегистрацииПриЗаписи(Источник, Отказ, Замещение) Экспорт
	Для Каждого Запись Из Источник Цикл
		Событие = Перечисления.рдв_События.ЗарегистрированоИзменение;
		СсылкаНаОбъект = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись.ВыгружаемыйОбъект, "СсылкаНаОбъект");
		РегистрыСведений.рдв_ЖурналСобытий.ЗарегистрироватьСобытие(Запись.ИдентификаторСобытия,
																	Запись.ИдентификаторСообщения,
																	Запись.ВыгружаемыйОбъект,
																	СсылкаНаОбъект,
																	Событие,
																	Запись.ВнешняяСистема,
																	Запись.ДатаРегистрации);
	КонецЦикла;
КонецПроцедуры

Процедура рдв_ЖурналСобытийПриЗаписиСообщенияПриЗаписи(Источник, Отказ) Экспорт
	
	Если Источник.ДополнительныеСвойства.Свойство("ЭтоНовый") Тогда
		Если Источник.Направление = Перечисления.рдв_НаправленияСообщенийИнтеграции.Исходящее Тогда
			Событие = Перечисления.рдв_События.ИсходящееСообщение;
		Иначе
			Событие = Перечисления.рдв_События.ВходящееСообщение;
		КонецЕсли;
		
		ОбъектыПоИдентификаторуСобытия = ОбъектыПоИдентификаторуСобытия(Источник.ИдентификаторСобытия);
		
		Если ОбъектыПоИдентификаторуСобытия.Количество() = 0 Тогда
		
			РегистрыСведений.рдв_ЖурналСобытий.ЗарегистрироватьСобытие(Источник.ИдентификаторСобытия,
																		Источник.ИдентификаторСообщения,
																		,
																		,
																		Событие,
																		Источник.ВнешняяСистема,
																		ТекущаяДатаСеанса(),
																		Источник.Ссылка);
		Иначе
			Для Каждого СсылкаНаОбъект Из ОбъектыПоИдентификаторуСобытия Цикл
				РегистрыСведений.рдв_ЖурналСобытий.ЗарегистрироватьСобытие(Источник.ИдентификаторСобытия,
																			Источник.ИдентификаторСообщения,
																			,
																			СсылкаНаОбъект,
																			Событие, 
																			Источник.ВнешняяСистема,
																			ТекущаяДатаСеанса(),
																			Источник.Ссылка);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	
КонецПроцедуры

Процедура рдв_ЖурналСобытийПередЗаписьюСообщенияПередЗаписью(Источник, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(Источник.Ссылка) Тогда
		Источник.ДополнительныеСвойства.Вставить("ЭтоНовый");
	КонецЕсли;
	
КонецПроцедуры

Функция ОбъектыПоИдентификаторуСобытия(ИдентификаторСобытия)

	МассивОбъектов = Новый Массив;
	Если ЗначениеЗаполнено(ИдентификаторСобытия) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	рдв_ЖурналСобытий.СсылкаНаОбъект КАК СсылкаНаОбъект
		|ИЗ
		|	РегистрСведений.рдв_ЖурналСобытий КАК рдв_ЖурналСобытий
		|ГДЕ
		|	рдв_ЖурналСобытий.ИдентификаторСобытия = &ИдентификаторСобытия
		|	И рдв_ЖурналСобытий.Событие = ЗНАЧЕНИЕ(Перечисление.рдв_События.ЗарегистрированоИзменение)";
		
		Запрос.УстановитьПараметр("ИдентификаторСобытия", ИдентификаторСобытия);
		МассивОбъектов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("СсылкаНаОбъект");
	КонецЕсли;
	Возврат МассивОбъектов;
	
КонецФункции

