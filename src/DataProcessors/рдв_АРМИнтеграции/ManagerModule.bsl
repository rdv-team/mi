
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Функция ОбновитьДанные(ПериодОбновления) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	рдв_ВнешниеСистемы.Ссылка КАК ВнешняяСистема,
	|	1 КАК Состояние
	|ИЗ
	|	Справочник.рдв_ВнешниеСистемы КАК рдв_ВнешниеСистемы
	|ГДЕ
	|	НЕ рдв_ВнешниеСистемы.ПометкаУдаления
	|	И НЕ рдв_ВнешниеСистемы.Ссылка = &ЭтотУзел
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВнешняяСистема
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СообщенияИнтеграции.ВидСообщения КАК ВидСообщения,
	|	КОЛИЧЕСТВО(1) КАК Количество,
	|	СообщенияИнтеграции.СтатусОбработки,
	|	СообщенияИнтеграции.Направление КАК Направление
	|ИЗ
	|	РегистрСведений.рдв_СообщенияИнтеграции КАК СообщенияИнтеграции
	|ГДЕ
	|	СообщенияИнтеграции.Направление = ЗНАЧЕНИЕ(Перечисление.рдв_НаправленияСообщенийИнтеграции.Входящее)
	|	И СообщенияИнтеграции.ДатаРегистрацииМс >= &ДатаМс
	|СГРУППИРОВАТЬ ПО
	|	СообщенияИнтеграции.ВидСообщения,
	|	СообщенияИнтеграции.СтатусОбработки,
	|	СообщенияИнтеграции.Направление
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВидСообщения
	|ИТОГИ
	|	СУММА(Количество)
	|ПО
	|	СтатусОбработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(1) КАК Количество,
	|	рдв_РегистрацияИзменений.ВидСообщения КАК ВидСообщения
	|ИЗ
	|	РегистрСведений.рдв_РегистрацияИзменений КАК рдв_РегистрацияИзменений
	//|ГДЕ
	//|	рдв_РегистрацияИзменений.ДатаРегистрации >= &Дата
	|ГДЕ
	|	рдв_РегистрацияИзменений.ДатаРегистрацииВМиллисекундах >= &ДатаМс
	|СГРУППИРОВАТЬ ПО
	|	рдв_РегистрацияИзменений.ВидСообщения
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВидСообщения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СообщенияИнтеграции.ВидСообщения КАК ВидСообщения,
	|	КОЛИЧЕСТВО(1) КАК Количество,
	|	СообщенияИнтеграции.СтатусОбработки КАК СтатусОбработки,
	|	СообщенияИнтеграции.Направление КАК Направление
	|ИЗ
	|	РегистрСведений.рдв_СообщенияИнтеграции КАК СообщенияИнтеграции
	|ГДЕ
	|	СообщенияИнтеграции.Направление = ЗНАЧЕНИЕ(Перечисление.рдв_НаправленияСообщенийИнтеграции.Исходящее)
	|	И СообщенияИнтеграции.ДатаРегистрацииМс >= &ДатаМс
	|СГРУППИРОВАТЬ ПО
	|	СообщенияИнтеграции.ВидСообщения,
	|	СообщенияИнтеграции.СтатусОбработки,
	|	СообщенияИнтеграции.Направление
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВидСообщения
	|ИТОГИ
	|	СУММА(Количество)
	|ПО
	|	СтатусОбработки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(рдв_СообщенияИнтеграции.ВидСообщения, ""Не классифицируемые"") КАК ВидСообщения,
	|	КОЛИЧЕСТВО(1) КАК Количество,
	|	рдв_СообщенияИнтеграции.ИдентификаторСообщения
	|ИЗ
	|	РегистрСведений.рдв_ОшибкиИнтеграции КАК ОшибкиИнтеграции
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.рдв_СообщенияИнтеграции КАК рдв_СообщенияИнтеграции
	|		ПО рдв_СообщенияИнтеграции.ИдентификаторСообщения = ОшибкиИнтеграции.ИдентификаторСообщения
	|ГДЕ
	|	ОшибкиИнтеграции.ДатаОшибки >= &Дата
	|СГРУППИРОВАТЬ ПО
	|	рдв_СообщенияИнтеграции.ВидСообщения,
	|	рдв_СообщенияИнтеграции.ИдентификаторСообщения
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВидСообщения
	|ИТОГИ
	|	СУММА(Количество)
	|ПО
	|	ВидСообщения";
	ТекущаяДата = ТекущаяДатаСеанса();
	ТекущаяДатаМс = ТекущаяУниверсальнаяДатаВМиллисекундах();
	Запрос.УстановитьПараметр("Дата", ТекущаяДата-(ПериодОбновления * 60));
	Запрос.УстановитьПараметр("ДатаМс", ТекущаяДатаМс-(ПериодОбновления * 60 * 1000));
	Запрос.УстановитьПараметр("ЭтотУзел", Справочники.рдв_ВнешниеСистемы.ЭтотУзел());
	
	Пакет = Запрос.ВыполнитьПакет();
	ВнешниеСистемы = Пакет[0].Выгрузить();
	АктуализироватьСостояниеВнешнихСистем(ВнешниеСистемы);
	
	ПолучениеОбработано = Пакет[1].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	ОтправкаИсходящаяОчередь = Пакет[2].Выгрузить();
	ОтправкаОтправлено = Пакет[3].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	Ошибки = Пакет[4].Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Результат = Новый Структура;
	Результат.Вставить("ВнешниеСистемы", ВнешниеСистемы);
	Результат.Вставить("Отправка", Новый Структура("Регистрация,ПоСтатусам", ОтправкаИсходящаяОчередь, ОтправкаОтправлено));
	Результат.Вставить("Получение", Новый Структура("ПоСтатусам", ПолучениеОбработано));
	Результат.Вставить("Ошибки", Ошибки);
	
	Возврат Результат;
	
КонецФункции

#Область ВнешниеСистемы

Процедура АктуализироватьСостояниеВнешнихСистем(ВнешниеСистемы)
	
	Для Каждого ВнешняяСистема Из ВнешниеСистемы Цикл
		
		Состояние = ВнешниеСистемыСостояние(ВнешняяСистема.ВнешняяСистема);
		ВнешняяСистема.Состояние = ?(Состояние=Истина, 0, 2);
		
	КонецЦикла;
	
КонецПроцедуры

Функция ВнешниеСистемыСостояние(ВнешняяСистема)
	
	Попытка
		Возврат рдв_Интеграция.ИнтеграцияВнешняяСистемаДоступна(ВнешняяСистема);
	Исключение
		Возврат Ложь;
	КонецПопытки; 
	
КонецФункции

#КонецОбласти

#КонецЕсли
