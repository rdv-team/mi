
&НаКлиенте
Процедура ВосстановитьОчереди(Команда)

	КлючиЗаписейОчередей = Элементы.Список.ВыделенныеСтроки;
	Если КлючиЗаписейОчередей.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не выделены строки для восстановления очередей.'"));
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'Записи очередей будут восстановлены в состоянии ""Новый"" в статусе ""Приостановлена"". Продолжить?'");

	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("КлючиЗаписейОчередей", КлючиЗаписейОчередей);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВосстановитьОчередиЗавершениеВопроса", ЭтаФорма, ПараметрыОповещения);
	
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);

КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьОчередиЗавершениеВопроса(Результат, ПараметрыОповещения) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;

	ВосстановитьОчередиНаСервере(ПараметрыОповещения.КлючиЗаписейОчередей);
	
КонецПроцедуры

&НаСервере
Процедура ВосстановитьОчередиНаСервере(Знач КлючиЗаписейОчередей)
	
	ИмяРегистраОчередей = рдв_РаботаСОчередямиКлиентСерверПереопределяемый.ИмяРегистраОчередей();
	ИмяРегистраЖурналаОчередей = рдв_РаботаСОчередямиКлиентСерверПереопределяемый.ИмяРегистраЖурналаОчередей();
	
	ТаблицаДанных = РегистрыСведений[ИмяРегистраЖурналаОчередей]
		.СоздатьНаборЗаписей().ВыгрузитьКолонки();

	// Заполнение таблицы ключей.
	Для Каждого КлючЗаписиОчереди Из КлючиЗаписейОчередей Цикл
		НоваяСтрока = ТаблицаДанных.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, КлючЗаписиОчереди);
	КонецЦикла;
	// RM-2481 14.07.2022 tosina@rdv-it.ru Закомментирована и заменена 1 строка. Переименование реквизитов журнала				
	//ТаблицаДанных.Сортировать("ДатаЖурнала, ДатаЖурналаМс");
	ТаблицаДанных.Сортировать("ДатаЖурналаUTC, ДатаЖурналаМс");
	
	// Удаление дублей строк по идентификаторам.
	МассивИдентификаторов = Новый Массив;
	КоличествоЗаписей = ТаблицаДанных.Количество();
	Для ОбратныйИндекс = 1 По КоличествоЗаписей Цикл
		СтрокаДанных = ТаблицаДанных[КоличествоЗаписей - ОбратныйИндекс];
		Если МассивИдентификаторов.Найти(СтрокаДанных.ИдентификаторЗаписи) <> Неопределено Тогда
			ТаблицаДанных.Удалить(СтрокаДанных);
		Иначе
			МассивИдентификаторов.Добавить(СтрокаДанных.ИдентификаторЗаписи);
		КонецЕсли;
	КонецЦикла;
	
	// Восстановление записвей очередей из журнала.
	Для Каждого СтрокаДанных Из ТаблицаДанных Цикл
		ЗаписьЖурнала = РегистрыСведений[ИмяРегистраЖурналаОчередей].СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(ЗаписьЖурнала, СтрокаДанных);
		ЗаписьЖурнала.Прочитать();
		Если ЗаписьЖурнала.Выбран() Тогда
			ЗаписьОчереди = РегистрыСведений[ИмяРегистраОчередей].СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(ЗаписьОчереди, ЗаписьЖурнала);
			ЗаписьОчереди.ДатаСозданияUTC = ТекущаяУниверсальнаяДата();
			// RM-2481 14.07.2022 tosina@rdv-it.ru Закомментирована и заменена 1 строка. Использовать в место "ТекущаяДата()" функцию "ТекущаяДатаСеанса()"				
			//ЗаписьОчереди.ДатаСоздания = ТекущаяДата();
			ЗаписьОчереди.ДатаСоздания = ТекущаяДатаСеанса();
			ЗаписьОчереди.Состояние = Перечисления.рдв_СостоянияОчередейОбработкиДанных.Новая;
			ЗаписьОчереди.ОписаниеСостояния = "Восстановлена из журнала";
			ЗаписьОчереди.КоличествоПопытокОбработки = 0;
			ЗаписьОчереди.ДатаСледующейПопыткиUTC = Дата(1, 1, 1);
			ЗаписьОчереди.ОстановленаОбработка = Истина;
			ЗаписьОчереди.ДатаИзмененияСостоянияUTC = Дата(1, 1, 1);
			ЗаписьОчереди.ИдентификаторПотока = "";
			ЗаписьОчереди.Записать();
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры
