
#Область ОбработчикиКоманд

&НаКлиенте
Процедура ОбработатьОчереди(Команда)
	
	КлючиЗаписейОчередей = Элементы.Список.ВыделенныеСтроки;
	Если КлючиЗаписейОчередей.Количество() = 0 Тогда
		ПоказатьПредупреждение(, "Не выбраны строки для обработки.");
		Возврат;
	КонецЕсли;
	
	ИмяРегистраОчередей = рдв_РаботаСОчередямиКлиентСерверПереопределяемый.ИмяРегистраОчередей();
	ОбработатьОчередиНаСервере(КлючиЗаписейОчередей);
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи." + ИмяРегистраОчередей));	
	ПоказатьПредупреждение(, "Обработка данных очередей завершена.");
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьОчередиНаСервере(Знач КлючиЗаписейОчередей)
	
	// Группировка по типам очередей.
	ТаблицаОчердей = Новый ТаблицаЗначений;
	ТаблицаОчердей.Колонки.Добавить("ТипОчереди");
	ТаблицаОчердей.Колонки.Добавить("ИдентификаторыОчереди");
	
	ТекущаяСтрокаОчереди = Неопределено;
	Для Каждого ЗаписьОчереди Из КлючиЗаписейОчередей Цикл
		Если ТекущаяСтрокаОчереди = Неопределено 
			Или ТекущаяСтрокаОчереди.ТипОчереди <> ЗаписьОчереди.ТипОчереди Тогда
			ТекущаяСтрокаОчереди = ТаблицаОчердей.Добавить();
			ТекущаяСтрокаОчереди.ТипОчереди = ЗаписьОчереди.ТипОчереди;
			ТекущаяСтрокаОчереди.ИдентификаторыОчереди = Новый Массив;
		КонецЕсли;
		ТекущаяСтрокаОчереди.ИдентификаторыОчереди.Добавить(ЗаписьОчереди.ИдентификаторЗаписи);
	КонецЦикла;
	
	// Запуск сгруппированных по типу очередей.
	Для Каждого СтрокаОчереди Из ТаблицаОчердей Цикл
		рдв_РаботаСОчередями.ВыполнитьОбработкуОчереди(СтрокаОчереди.ТипОчереди,
			СтрокаОчереди.ИдентификаторыОчереди);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСостояние(Команда)
	
	КлючиЗаписейОчередей = Элементы.Список.ВыделенныеСтроки;
	Если КлючиЗаписейОчередей.Количество() = 0 Тогда
		ПоказатьПредупреждение(, "Не выбраны строки для установки состояния.");
		Возврат;
	КонецЕсли;
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("КлючиЗаписейОчередей", КлючиЗаписейОчередей);
	ОписаниеОповещения = Новый ОписаниеОповещения("УстановитьСостояниеВыборСостояния", ЭтаФорма, ПараметрыОповещения);
	СостояниеПоУмолчанию = ПредопределенноеЗначение("Перечисление.рдв_СостоянияОчередейОбработкиДанных.Новая");

	ПоказатьВводЗначения(
		ОписаниеОповещения,
		СостояниеПоУмолчанию,
		НСтр("ru = 'Выборка состояния записей очередей'"),
		Тип("ПеречислениеСсылка.рдв_СостоянияОчередейОбработкиДанных")
	);

КонецПроцедуры

&НаКлиенте
Процедура ОстановитьОчереди(Команда)
	УстановитьФлагОстановкиОчереди(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьОстановкуОчередей(Команда)
	УстановитьФлагОстановкиОчереди(Ложь);
КонецПроцедуры

#КонецОбласти

#Область УправлениеСостояниемОчередей

&НаКлиенте
Процедура УстановитьСостояниеВыборСостояния(Результат, ПараметрыОповещения) Экспорт

	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеСостояния = НСтр("ru = 'Изменение состояния из списка записей очередей'");
	
	УстановитьСостояниеЗаписейОчередей(
		ПараметрыОповещения.КлючиЗаписейОчередей,
		Результат, ОписаниеСостояния
	);
	
	ИмяРегистраОчередей = рдв_РаботаСОчередямиКлиентСерверПереопределяемый.ИмяРегистраОчередей();
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи." + ИмяРегистраОчередей));	

КонецПроцедуры
	
&НаСервере
Процедура УстановитьСостояниеЗаписейОчередей(Знач КлючиЗаписейОчередей, Состояние, ОписаниеСостояния = "")
	Если Состояние = Перечисления.рдв_СостоянияОчередейОбработкиДанных.Новая Тогда
		ДатаСледующейПопыткиUTC = Дата(1, 1, 1);
		ОстановленаОбработка = Ложь;
	Иначе
		ДатаСледующейПопыткиUTC = Неопределено;
		ОстановленаОбработка = Неопределено;
	КонецЕсли;
	Для Каждого КлючЗаписиОчереди Из КлючиЗаписейОчередей Цикл
		рдв_РаботаСОчередями.ИзменитьСостояниеОчереди(
			КлючЗаписиОчереди.ТипОчереди,
			КлючЗаписиОчереди.ИдентификаторЗаписи,
			Состояние,
			ОписаниеСостояния,
			ДатаСледующейПопыткиUTC,
			ОстановленаОбработка
		);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлагОстановкиОчереди(Остановлена)

	КлючиЗаписейОчередей = Элементы.Список.ВыделенныеСтроки;
	Если КлючиЗаписейОчередей.Количество() = 0 Тогда
		ПоказатьПредупреждение(, "Не выбраны строки для изменения флага остановки обработки очередей.");
		Возврат;
	КонецЕсли;
	
	Если Остановлена Тогда
		ТекстВопроса = НСтр("ru = 'Выбранные очереди не будут обрабатываться после установки флага остановки. Продожить?'");
	Иначе
		ТекстВопроса = НСтр("ru = 'У выбранных очередей будет снят флаг остановки, очереди будут отправлены в обработку. Продожить?'");
	КонецЕсли;
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("КлючиЗаписейОчередей", КлючиЗаписейОчередей);
	ПараметрыОповещения.Вставить("ОстановленаОбработка", Остановлена);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("УстановитьФлагОстановкиОчередиВопросЗавершение", ЭтаФорма, ПараметрыОповещения);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлагОстановкиОчередиВопросЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьФлагОстановкиОчередиНаСервере(
		ПараметрыОповещения.КлючиЗаписейОчередей,
		ПараметрыОповещения.ОстановленаОбработка
	);
	
	ИмяРегистраОчередей = рдв_РаботаСОчередямиКлиентСерверПереопределяемый.ИмяРегистраОчередей();
	ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи." + ИмяРегистраОчередей));	

КонецПроцедуры

&НаСервере
Процедура УстановитьФлагОстановкиОчередиНаСервере(КлючиЗаписейОчередей, Остановлена)
	Если Не Остановлена Тогда
		ДатаСледующейПопыткиUTC = Дата(1, 1, 1);
	Иначе
		ДатаСледующейПопыткиUTC = Неопределено;
	КонецЕсли;
	Для Каждого КлючЗаписиОчереди Из КлючиЗаписейОчередей Цикл
		рдв_РаботаСОчередями.ИзменитьСостояниеОчереди(
			КлючЗаписиОчереди.ТипОчереди,
			КлючЗаписиОчереди.ИдентификаторЗаписи,,,
			ДатаСледующейПопыткиUTC,
			Остановлена
		);
	КонецЦикла;
КонецПроцедуры

#КонецОбласти
