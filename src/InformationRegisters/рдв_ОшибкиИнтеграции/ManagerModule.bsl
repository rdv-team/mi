
Процедура ЗаписатьОшибкуИнтеграции(ИнформацияОбОшибке, Контекст) Экспорт
	
	ДатаОшибкиМс = ТекущаяУниверсальнаяДатаВМиллисекундах();
	ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	ДатаЗаписиНачалоЧаса = НачалоЧаса(ТекущаяДатаСеанса);
	
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.ИдентификаторСообщения.Установить(Контекст.ИдентификаторСообщения);
	Набор.Отбор.ДатаОшибкиМс.Установить(ДатаОшибкиМс);
	Набор.Отбор.ДатаЗаписиНачалоЧаса.Установить(ДатаЗаписиНачалоЧаса);
	
	Запись = Набор.Добавить();
	Запись.ИдентификаторСообщения = Контекст.ИдентификаторСообщения;
	Запись.ДатаОшибкиМс = ДатаОшибкиМс;
	Запись.ДатаЗаписиНачалоЧаса = ДатаЗаписиНачалоЧаса;
	Запись.ТипОшибки = ?(ЗначениеЗаполнено(Контекст.ТипОшибки)
											, Контекст.ТипОшибки
											, Перечисления.рдв_ТипыОшибокИнтеграции.ОбщаяОшибка);
	Запись.ДатаОшибки = ТекущаяДатаСеанса;
	
	Если ТипЗнч(ИнформацияОбОшибке) = Тип("Строка") Тогда
		ПредставлениеОшибки = ИнформацияОбОшибке;
	Иначе
		ПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КонецЕсли;
	
	Запись.ОписаниеОшибки = СтрШаблон("Ошибка события:%1%2%3", Контекст.Событие, Символы.ПС
													, ПредставлениеОшибки);
	
	Набор.Записать();
	
КонецПроцедуры