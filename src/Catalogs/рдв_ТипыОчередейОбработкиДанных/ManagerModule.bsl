#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ИнициализироватьТипыОчередей() Экспорт
	
	СписокОчередей = Новый СписокЗначений;
	рдв_РаботаСОчередямиПереопределяемый.ЗаполнитьСписокОчередей(СписокОчередей);
	
	Для Каждого ЭлементСписка Из СписокОчередей Цикл
		
		ИмяОчереди = ЭлементСписка.Значение;
		ПредставлениеОчереди = ЭлементСписка.Представление; 
		
		//@skip-check query-in-loop
		ТипОчередиСсылка = НайтиПоИмени(ИмяОчереди);
		
		Если ЗначениеЗаполнено(ТипОчередиСсылка) Тогда
			ТипОчередиОбъект = ТипОчередиСсылка.ПолучитьОбъект();
		Иначе
			ТипОчередиОбъект = СоздатьЭлемент();
		КонецЕсли;
		
		ТипОчередиОбъект.ИмяОчереди = ИмяОчереди;
		ТипОчередиОбъект.Наименование = ПредставлениеОчереди;
		
		рдв_РаботаСОчередями.ЗаполнитьТипОчередиПоУмолчанию(ТипОчередиОбъект);
		
		ТипОчередиОбъект.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьРасписаниеОчереди(ОбъектОчереди, РасписаниеОчереди, ИспользованиеРегламента) Экспорт
	
	ЗаполнитьРасписаниеОчереди(ОбъектОчереди, РасписаниеОчереди, ИспользованиеРегламента);
	
	рдв_РаботаСОчередями.УстановитьРасписаниеОработкиОчереди(
		ОбъектОчереди, РасписаниеОчереди, ИспользованиеРегламента);	
	
КонецПроцедуры

Процедура ЗаполнитьРасписаниеОчереди(ОбъектОчереди, РасписаниеОчереди, ИспользованиеРегламента) Экспорт
	
	Если ОбъектОчереди.ПростаяНастройкаРегламента Тогда
		МинПериодРегламентаСек = рдв_РаботаСОчередями.МинимальныйПериодЗапускаРегламента();
		Если ОбъектОчереди.ПериодОбработкиОчередиСек < МинПериодРегламентаСек Тогда
			РасписаниеОчереди = рдв_РегламентныеЗаданияКлиентСервер.РасписаниеЧастоПовторяемогоЗадания(
				ОбъектОчереди.ПериодОбработкиОчередиСек,
				ОбъектОчереди.ВремяЖизниЗаданияОбработкиМин);
		Иначе
			РасписаниеОчереди = рдв_РегламентныеЗаданияКлиентСервер.ПростоеРасписание(
				ОбъектОчереди.ПериодОбработкиОчередиСек);
		КонецЕсли;
	КонецЕсли;
		
	ПредставлениеРасписания = "";
	Если ИспользованиеРегламента Тогда
		Если ОбъектОчереди.ПростаяНастройкаРегламента Тогда
			РасписаниеДляПредставления = Новый РасписаниеРегламентногоЗадания;
			РасписаниеДляПредставления.ПериодПовтораДней = 1;
			РасписаниеДляПредставления.ПериодПовтораВТечениеДня = ОбъектОчереди.ПериодОбработкиОчередиСек;
			ПредставлениеРасписания = Строка(РасписаниеДляПредставления);
		Иначе
			ПредставлениеРасписания = Строка(РасписаниеОчереди);
		КонецЕсли;
	Конецесли;
	
	ОбъектОчереди.ПредставлениеРасписания = ПредставлениеРасписания;

КонецПроцедуры

Функция НайтиПоИмени(ИмяОчереди) Экспорт
	
	ТипОчереди = НайтиПоРеквизиту("ИмяОчереди", ИмяОчереди);
	
	Если Не ЗначениеЗаполнено(ТипОчереди) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТипыОчередей.ИмяПредопределенныхДанных КАК ИмяПредопределенныхДанных
		|ИЗ
		|	Справочник.рдв_ТипыОчередейОбработкиДанных КАК ТипыОчередей
		|ГДЕ
		|	ТипыОчередей.Предопределенный";
		ИменаПредопределенныхДанных = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ИмяПредопределенныхДанных");
		Если ИменаПредопределенныхДанных.Найти(ИмяОчереди) <> Неопределено Тогда
			ТипОчереди = Справочники.рдв_ТипыОчередейОбработкиДанных[ИмяОчереди];
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТипОчереди;
	
КонецФункции

Функция ИмяОчереди(ТипОчереди) Экспорт
	// ТипОчереди - Ссылка, Объект.
	Если Не ПустаяСтрока(ТипОчереди.ИмяОчереди) Тогда
		Возврат ТипОчереди.ИмяОчереди;
	КонецЕсли;
	Возврат ТипОчереди.ИмяПредопределенныхДанных;	
КонецФункции

#КонецОбласти

#Область ОбработчикиОбновления

Процедура ОбновитьПользователейОбработкиОчередей() Экспорт
	
	ТипОчереди = Справочники.рдв_ТипыОчередейОбработкиДанных.Выбрать();
	
	Пока ТипОчереди.Следующий() Цикл
		
		Если Не ЗначениеЗаполнено(ТипОчереди.РегламентноеЗаданиеGUID) Тогда
			Продолжить;
		ИначеЕсли Не ПустаяСтрока(ТипОчереди.ИмяПользователяРегламента) Тогда
			Продолжить;
		КонецЕсли;
		
		СвойстваЗадания = рдв_РаботаСОчередями.РасписаниеОбработкиОчереди(ТипОчереди.Ссылка);		
		Если Не СвойстваЗадания.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Попытка
			
			Объект = ТипОчереди.ПолучитьОбъект();
			Если ПустаяСтрока(Объект.ИмяПользователяРегламента) Тогда
				рдв_РаботаСОчередями.УстановитьРасписаниеОработкиОчереди(
					Объект,
					СвойстваЗадания.Расписание,
					СвойстваЗадания.Использование
				);
				Объект.Записать();
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ВызватьИсключение;
			
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьИменаПредопределенныхОчередей() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТипыОчередей.Ссылка КАК Ссылка,
	|	ТипыОчередей.ИмяОчереди КАК ИмяОчереди,
	|	ТипыОчередей.ИмяПредопределенныхДанных КАК ИмяПредопределенныхДанных
	|ИЗ
	|	Справочник.рдв_ТипыОчередейОбработкиДанных КАК ТипыОчередей
	|ГДЕ
	|	ТипыОчередей.Предопределенный";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ИмяОчереди = Выборка.ИмяПредопределенныхДанных Тогда
			Продолжить;
		КонецЕсли;
		
		Объект = Выборка.Ссылка.ПолучитьОбъект();
		Объект.ИмяОчереди = Выборка.ИмяПредопределенныхДанных;
		Объект.Записать();
				
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
