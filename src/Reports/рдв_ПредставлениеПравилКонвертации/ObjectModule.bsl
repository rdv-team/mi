
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);
	
	Данные = ВнешнийНаборДанныхПравилКонвертации();
	ВнешнийНабор = Новый Структура;
	ВнешнийНабор.Вставить("Правила", Данные);
	
	//Инициализируем процессор компоновки
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешнийНабор, ДанныеРасшифровки, Истина);
	
	//Очищаем документ результата
	ДокументРезультат.Очистить();
	
	//Выводим отчет в документ
	ПроцессорВывода = Новый
	ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);	

КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВнешнийНаборДанныхПравилКонвертации()
	
	Отправка = рдв_МенеджерОбмена.ИнициализироватьПравилаКонвертации("Отправка"); // ТаблицаЗначений
	Получение = рдв_МенеджерОбмена.ИнициализироватьПравилаКонвертации("Получение"); // ТаблицаЗначений
	
	ТаблицаПравил = Новый ТаблицаЗначений;
	ТаблицаПравил.Колонки.Добавить("ИмяОбъекта");
	ТаблицаПравил.Колонки.Добавить("ИмяПКООбъекта");
	ТаблицаПравил.Колонки.Добавить("ИмяПКО");
	ТаблицаПравил.Колонки.Добавить("Раздел");
	ТаблицаПравил.Колонки.Добавить("Направление");
	ТаблицаПравил.Колонки.Добавить("ИмяКлючаФормата");
	ТаблицаПравил.Колонки.Добавить("ИмяРеквизита");
	ТаблицаПравил.Колонки.Добавить("Обязательный");
	ТаблицаПравил.Колонки.Добавить("МетаданныеОбъекта");
	ТаблицаПравил.Колонки.Добавить("ПоляПоиска");
	
	ЗаполнитьПоПравилам(Отправка, ТаблицаПравил, "Отправка");
	ЗаполнитьПоПравилам(Получение, ТаблицаПравил, "Получение");
	
	Возврат ТаблицаПравил;
	
КонецФункции

Процедура ЗаполнитьПоПравилам(Правила, ТаблицаПравил, Направление)
	
	Для Каждого Правило Из Правила Цикл
		
		НоваяСтрокаПравил = ТаблицаПравил.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаПравил, Правило, , "МетаданныеОбъекта,ПоляПоиска");
		НоваяСтрокаПравил.Направление = Направление;
		НоваяСтрокаПравил.Раздел = "Правило";
		НоваяСтрокаПравил.МетаданныеОбъекта = Правило.МетаданныеОбъекта.ПолноеИмя();
		НоваяСтрокаПравил.ИмяПКООбъекта = Правило.ИмяПКО;
		
		Если ЗначениеЗаполнено(Правило.ПоляПоиска) Тогда
			НоваяСтрокаПоиска = ТаблицаПравил.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаПоиска, НоваяСтрокаПравил);
			НоваяСтрокаПоиска.Раздел = "Поля поиска";
			НоваяСтрокаПоиска.ПоляПоиска = Правило.ПоляПоиска;
			НоваяСтрокаПоиска.ИмяПКООбъекта = Правило.ИмяПКО;
		КонецЕсли;
		
		Для Каждого Шапка Из Правило.КонвертацияШапки Цикл
			
			НоваяСтрокаШапки = ТаблицаПравил.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаШапки, Шапка);
			НоваяСтрокаШапки.Направление = Направление;
			НоваяСтрокаШапки.Раздел = "Шапка";
			НоваяСтрокаШапки.ИмяОбъекта = Правило.ИмяОбъекта;
			НоваяСтрокаШапки.МетаданныеОбъекта = Правило.МетаданныеОбъекта.ПолноеИмя();
			НоваяСтрокаШапки.ИмяПКООбъекта = Правило.ИмяПКО;
			
		КонецЦикла;
		 
		Для Каждого ТабличныеЧасти Из Правило.КонвертацияТабличныхЧастей Цикл
			
			Для Каждого СвойстваТЧ Из ТабличныеЧасти.КонвертацияСвойств Цикл
				СтрокаТЧ = ТаблицаПравил.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТЧ, СвойстваТЧ);
				СтрокаТЧ.Направление = Направление;
				СтрокаТЧ.Раздел = СтрШаблон("%1 КАК %2", ТабличныеЧасти.ИмяТабличнойЧасти, ТабличныеЧасти.ИмяКлючаФормата);
				СтрокаТЧ.ИмяОбъекта = Правило.ИмяОбъекта;
				СтрокаТЧ.МетаданныеОбъекта = Правило.МетаданныеОбъекта.ПолноеИмя();
				СтрокаТЧ.ИмяПКООбъекта = Правило.ИмяПКО;
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
