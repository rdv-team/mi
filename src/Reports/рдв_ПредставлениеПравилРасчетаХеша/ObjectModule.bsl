
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);
	
	Данные = ВнешнийНаборДанныхПравилКонвертации();
	ВнешнийНабор = Новый Структура;
	ВнешнийНабор.Вставить("Правила", Данные);
	
	//Инициализируем процессор компоновки
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешнийНабор, ДанныеРасшифровки, Истина);
	
	//Очищаем документ результата
	ДокументРезультат.Очистить();
	
	//Выводим отчет в документ
	ПроцессорВывода = Новый
	ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных);	

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВнешнийНаборДанныхПравилКонвертации()

	ТаблицаПравил = Новый ТаблицаЗначений;
	ТаблицаПравил.Колонки.Добавить("ВидХеша");
	ТаблицаПравил.Колонки.Добавить("ИмяОбъекта");
	ТаблицаПравил.Колонки.Добавить("ИмяПРХОбъекта");
	ТаблицаПравил.Колонки.Добавить("МетаданныеОбъекта");
	ТаблицаПравил.Колонки.Добавить("ИмяПРХ");
	ТаблицаПравил.Колонки.Добавить("ИмяРеквизита");
	ТаблицаПравил.Колонки.Добавить("Раздел");

	Правила = рдв_ФормированиеХешейОбъектовПовтИсп.ИнициализироватьПравилаРасчетаХеша();
	
	ЗаполнитьПоПравилам(Правила, ТаблицаПравил);
	
	Возврат ТаблицаПравил;
	
КонецФункции

Процедура ЗаполнитьПоПравилам(Правила, ТаблицаПравил)
	
	Для Каждого Правило Из Правила Цикл
		
		НоваяСтрокаПравил = ТаблицаПравил.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаПравил, Правило, , "МетаданныеОбъекта");
		НоваяСтрокаПравил.Раздел = "Правило";
		НоваяСтрокаПравил.МетаданныеОбъекта = Правило.МетаданныеОбъекта.ПолноеИмя();
		НоваяСтрокаПравил.ИмяПРХОбъекта = Правило.ИмяПРХ;
		
		Для Каждого Шапка Из Правило.КонвертацияШапки Цикл
			
			НоваяСтрокаШапки = ТаблицаПравил.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаШапки, Шапка);
			НоваяСтрокаШапки.Раздел = "Шапка";
			НоваяСтрокаШапки.ИмяОбъекта = Правило.ИмяОбъекта;
			НоваяСтрокаШапки.МетаданныеОбъекта = Правило.МетаданныеОбъекта.ПолноеИмя();
			НоваяСтрокаШапки.ИмяПРХОбъекта = Правило.ИмяПРХ;
			НоваяСтрокаШапки.ВидХеша = Правило.ВидХеша;
			
		КонецЦикла;
		 
		Для Каждого ТабличныеЧасти Из Правило.КонвертацияТабличныхЧастей Цикл
			
			Для Каждого СвойстваТЧ Из ТабличныеЧасти.КонвертацияСвойств Цикл
				СтрокаТЧ = ТаблицаПравил.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТЧ, СвойстваТЧ);
				СтрокаТЧ.Раздел = ТабличныеЧасти.ИмяТабличнойЧасти;
				СтрокаТЧ.ИмяОбъекта = Правило.ИмяОбъекта;
				СтрокаТЧ.МетаданныеОбъекта = Правило.МетаданныеОбъекта.ПолноеИмя();
				СтрокаТЧ.ИмяПРХОбъекта = Правило.ИмяПРХ;
				СтрокаТЧ.ВидХеша = Правило.ВидХеша;
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
